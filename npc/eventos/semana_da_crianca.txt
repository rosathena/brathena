//    ________________________________________________________ 
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: semana_da_criança.txt                  |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.1/1.1/1.0                         |
//   |--------------------------------------------------------|
//   | Descrição: Evento da Semana da Criança do IndiaRO.     |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [Holy]                              |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

prontera,146,91,5	script	Pandit Chacha#crianca07	1_M_PUBMASTER,{
	if (BaseLevel > 50) {
		mes "[Pandit Chacha]";
		mes "Hahaha~";
		mes "É um dia especial, o festejamos apenas uma vez por ano.";
		mes "Se você encontrar pequenos aventureiros mande-os até mim~";
		mes "Eu irei dá-los um presente especial.";
		close;
	} else if (BaseLevel < 20) {
		mes "[Pandit Chacha]";
		mes "Hahaha~";
		mes "Você é um bebê aventureiro não um pequeno aventureiro.";
		mes "Quando você crescer mais, volte aqui novamente. Hahaha.";
		close;
	} else if (evento_semana_crianca9 < 1) {
		mes "[Pandit Chacha]";
		mes "Hahaha~";
		mes "Bem-vindo, pequeno aventureiro!";
		mes "Hoje é um dia especial, não acha~?";
		mes "Eu gostaria de dar um pequeno presente para algum aventureiro...";
		mes "E você? Você quer levá-lo?";
		next;
		if (select("Não. Eu volto mais tarde.:Claro, eu quero.") == 1) {
			mes "[Pandit Chacha]";
			mes "Isso é muito ruim.... hum...";
			mes "Juntei alguns animais para formar o mundo....";
			mes "Volte sempre que quiser...";
			close;
		}
		set evento_semana_crianca9,1;
		getitem 11705,10; //Poção Infantil
		mes "[Pandit Chacha]";
		mes "Veja. É uma poção infantil.";
		mes "O peso é apenas 1 e cura uma quantidade significativa de HP.";
		mes "Se você deseja ganhar mais delas, traga-me 1 Buquê de casamento e 1 Rosa Eterna.";
		close;
	} else if (evento_semana_crianca9 == 1) {
		if (countitem(745) > 0 && countitem(748) > 0) {
			mes "[Pandit Chacha]";
			mes "Ahha!!";
			mes "Você me lembra a minha bela composição.";
			mes "Você fez um bom trabalho.";
			mes "Trouxe 1 Buquê de Casamento e 1 Rosa Eterna? ";
			next;
			if (select("Não agora.:Claro, pegue.") == 1) {
				mes "[Pandit Chacha]";
				mes "Se você não está preparado ainda, me ligue quando você estiver pronto.";
				close;
			}
			mes "[Pandit Chacha]";
			mes "Hahaha~";
			mes "Ah~ você trouxe.";
			mes "Aqui, irei trocar por 50 Poções Infantis.";
			mes "Depois de conseguir as 50 poções infantis, eu estou sem mais nenhuma delas.";
			close2;
			delitem 745,1; // Buquê de Casamento
			delitem 748,1; // Rosa Eterna
			set evento_semana_crianca9,2;
			getitem 11705,50; // Poção Infantil
			end;
		}
		mes "[Pandit Chacha]";
		mes "Pequeno aventureiro, você deve me trazer 1 Buquê de Casamento e 1 Rosa Eterna.";
		mes "Se você me trouxer esses itens irei trocá-los por Poções Infantis. ";
		close;
	}
	mes "[Pandit Chacha]";
	mes "Hahaha~";
	mes "Você está gostando da semana da criança~?";
	close;
}