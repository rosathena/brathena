//    ________________________________________________________ 
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Guilda de Aprendizes                   |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.3/1.3/1.2                         |
//   |--------------------------------------------------------|
//   | Descrição: Bandeiras,Cavernas,Funções,Gerenciadores,   |
//   | Guardiões, Kafras, Portais e Tesouros.                 |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [cleyton]                           |
//   | 1.2 União de NPCs [Holy]                               |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

// Funções das Bandeiras
//============================================================
function	script	F_Flags	{
	set .@GID, GetCastleData(getarg(1),1);
	if (getarg(5) == 0) return;
	if (.@GID == 0) {
		mes " [ Decreto do Reino de Midgard ]";
		mes " ";
		mes "1. Conforme o pacto do Reino de Midgard, declaro que este Castelo não possui um Senhor.";
		mes " ";
		mes "2. Aquele que superar terríveis desafios e destruir o Emperium, receberá a posse do castelo com o apoio do nosso rei.";
		return;
	}
	else {
		if (getcharid(2) == .@GID && getarg(4) == 1) {
			mes "Bravo guerreiro...";
			mes "Você deseja retornar ao seu lugar de honra?";
			next;
			if (select("Retornar para o castelo do Clã.:Não.") == 1) {
				if (getcharid(2) == GetCastleData(getarg(1),1))
					warp getarg(1),getarg(2),getarg(3);
			}
			return;
		}
		mes "[ Decreto do Reino de Midgard ]";
		mes " ";
		mes "1. Seguindo o decreto do Reino de Midgard:";
		mes "Nós declaramos que esse castelo pertence ao clã ^ff0000" + GetGuildName(.@GID) + "^000000.";
		mes " ";
		mes "2. O líder do clã ^ff0000"+ GetGuildName(.@GID) + "^000000 se chama ^FF0000" + GetGuildMaster(.@GID) + "^000000.";
		mes "Se alguém se opõe a esse decreto, que prove sua força e honra com uma espada em suas mãos.";
		return;
	}
}

//============================================================================//
// 			1° Castelo
//============================================================================//
n_castle,110,96,1	script	NGuild Aldebaran#a1-1	GUILD_FLAG,{
	callfunc "F_Flags","Al De Baran","nguild_alde",218,170,1,1;
	close;

OnRecvCastleN01:
	FlagEmblem GetCastleData("nguild_alde",1);
	end;
}

nguild_alde,30,248,4	script	NGuild Aldebaran#a1-6::NGuildAlde	GUILD_FLAG,{
	callfunc "F_Flags","Al De Baran","nguild_alde",218,170,0,1;
	close;

OnRecvCastleN01:
	FlagEmblem GetCastleData("nguild_alde",1);
	end;
}
// Dentro do Castelo
//============================================================
nguild_alde,30,246,4	duplicate(NGuildAlde)	NGuild Aldebaran#a1-7	GUILD_FLAG
nguild_alde,37,248,4	duplicate(NGuildAlde)	NGuild Aldebaran#a1-8	GUILD_FLAG
nguild_alde,37,246,4	duplicate(NGuildAlde)	NGuild Aldebaran#a1-9	GUILD_FLAG
nguild_alde,95,80,2	duplicate(NGuildAlde)	NGuild Aldebaran#a1-10	GUILD_FLAG
nguild_alde,95,59,2	duplicate(NGuildAlde)	NGuild Aldebaran#a1-11	GUILD_FLAG
nguild_alde,62,75,2	duplicate(NGuildAlde)	NGuild Aldebaran#a1-12	GUILD_FLAG
nguild_alde,70,75,2	duplicate(NGuildAlde)	NGuild Aldebaran#a1-13	GUILD_FLAG
nguild_alde,74,75,2	duplicate(NGuildAlde)	NGuild Aldebaran#a1-14	GUILD_FLAG
nguild_alde,62,64,2	duplicate(NGuildAlde)	NGuild Aldebaran#a1-15	GUILD_FLAG
nguild_alde,66,64,2	duplicate(NGuildAlde)	NGuild Aldebaran#a1-16	GUILD_FLAG
nguild_alde,70,64,2	duplicate(NGuildAlde)	NGuild Aldebaran#a1-17	GUILD_FLAG
nguild_alde,74,64,2	duplicate(NGuildAlde)	NGuild Aldebaran#a1-18	GUILD_FLAG
nguild_alde,203,150,4	duplicate(NGuildAlde)	NGuild Aldebaran#a1-19	GUILD_FLAG
nguild_alde,210,150,4	duplicate(NGuildAlde)	NGuild Aldebaran#a1-20	GUILD_FLAG

//============================================================================//
// 			2° Castelo
//============================================================================//
n_castle,110,109,3	script	NGuild Geffen#g1-1	GUILD_FLAG,{
	callfunc "F_Flags","Geffen","nguild_gef",83,47,1,1;
	close;

OnRecvCastleN02:
	FlagEmblem GetCastleData("nguild_gef",1);
	end;
}

nguild_gef,28,157,4	script	NGuild Geffen#g1-6::NGuildGef	GUILD_FLAG,{
	callfunc "F_Flags","Geffen","nguild_gef",83,47,0,1;
	close;

OnRecvCastleN02:
	FlagEmblem GetCastleData("nguild_gef",1);
	end;
}

// Dentro do Castelo
//============================================================
nguild_gef,32,157,4	duplicate(NGuildGef)	NGuild Geffen#g1-7	GUILD_FLAG
nguild_gef,22,156,5	duplicate(NGuildGef)	NGuild Geffen#g1-8	GUILD_FLAG
nguild_gef,68,185,3	duplicate(NGuildGef)	NGuild Geffen#g1-9	GUILD_FLAG
nguild_gef,17,171,5	duplicate(NGuildGef)	NGuild Geffen#g1-10	GUILD_FLAG
nguild_gef,59,16,4	duplicate(NGuildGef)	NGuild Geffen#g1-11	GUILD_FLAG
nguild_gef,64,16,4	duplicate(NGuildGef)	NGuild Geffen#g1-12	GUILD_FLAG

//============================================================================//
// 			3° Castelo
//============================================================================//
n_castle,94,109,5	script	NGuild Payon#f1-1	GUILD_FLAG,{
	callfunc "F_Flags","Payon","nguild_pay",87,29,1,1;
	close;

OnRecvCastleN03:
	FlagEmblem GetCastleData("nguild_pay",1);
	end;
}

nguild_pay,238,67,4	script	NGuild Payon#f1-6::NGuildPay	GUILD_FLAG,{
	callfunc "F_Flags","Payon","nguild_pay",87,29,0,1;
	close;

OnRecvCastleN03:
	FlagEmblem GetCastleData("nguild_pay",1);
	end;
}

// Dentro do Castelo
//============================================================
nguild_pay,238,67,4	duplicate(NGuildPay)	NGuild Payon#f1-6	GUILD_FLAG
nguild_pay,233,67,4	duplicate(NGuildPay)	NGuild Payon#f1-7	GUILD_FLAG
nguild_pay,221,123,4	duplicate(NGuildPay)	NGuild Payon#f1-8	GUILD_FLAG
nguild_pay,221,116,4	duplicate(NGuildPay)	NGuild Payon#f1-9	GUILD_FLAG
nguild_pay,206,108,4	duplicate(NGuildPay)	NGuild Payon#f1-10	GUILD_FLAG
nguild_pay,212,108,4	duplicate(NGuildPay)	NGuild Payon#f1-11	GUILD_FLAG

//=============================================================================//
// 			4° Castelo
//=============================================================================//
n_castle,94,96,7	script	NGuild Prontera#p1-1	GUILD_FLAG,{
	callfunc "F_Flags","Prontera","nguild_prt",97,174,1,1;
	close;

OnRecvCastleN04:
	FlagEmblem GetCastleData("nguild_prt",1);
	end;
}

nguild_prt,58,56,4	script	NGuild Prontera#p1-7::NGuildPrt	GUILD_FLAG,{
	callfunc "F_Flags","Prontera","nguild_prt",97,174,0,1;
	close;

OnRecvCastleN04:
	FlagEmblem GetCastleData("nguild_prt",1);
	end;
}

// Dentro do Castelo
//============================================================
nguild_prt,64,56,4	duplicate(NGuildPrt)	NGuild Prontera#p1-8	GUILD_FLAG
nguild_prt,76,32,4	duplicate(NGuildPrt)	NGuild Prontera#p1-9	GUILD_FLAG
nguild_prt,84,32,4	duplicate(NGuildPrt)	NGuild Prontera#p1-10	GUILD_FLAG
nguild_prt,94,39,4	duplicate(NGuildPrt)	NGuild Prontera#p1-11	GUILD_FLAG
nguild_prt,94,24,4	duplicate(NGuildPrt)	NGuild Prontera#p1-12	GUILD_FLAG
nguild_prt,73,14,4	duplicate(NGuildPrt)	NGuild Prontera#p1-13	GUILD_FLAG
nguild_prt,73,6,4	duplicate(NGuildPrt)	NGuild Prontera#p1-14	GUILD_FLAG
nguild_prt,55,46,4	duplicate(NGuildPrt)	NGuild Prontera#p1-15	GUILD_FLAG
nguild_prt,45,46,4	duplicate(NGuildPrt)	NGuild Prontera#p1-16	GUILD_FLAG

// Função das Cavernas
//==================================================
function	script	F_GldDunSw	{
	set .@GID, GetCastleData(getarg(0),1);
	if (.@GID == 0) {
		mes "[ Voz ecoando ]";
		mes " ' Aquele que superar as provações com bravura... encontrará o caminho... ' ";
		close;
	}
	else {
		mes "[ Voz ecoando ]";
		mes " ' Somente aquele que demonstrar bravura pode superar as provações. '";
		next;
		mes " ";
		mes "Há uma pequena alavanca aqui.";
		mes "Você deseja puxar ela?";
		next;

		if (select("Puxar.:Não puxar.") == 1) {
			if (getcharid(2) == .@GID) {
				warp "gld_dun"+getarg(1),getarg(2),getarg(3);
				end;
			}
			mes " ";
			mes " Não aconteceu nada.";
		}
		return;
	}
}

// 1° Castelo
//============================================================
nguild_alde,212,181,0	script	Switch#DunN01	HIDDEN_NPC,{
	callfunc "F_GldDunSw","nguild_alde","02",32,122;
	close;
}

// 2° Castelo
//============================================================
nguild_gef,78,84,0	script	Switch#DunN02	HIDDEN_NPC,{
	callfunc "F_GldDunSw","nguild_gef","04",39,258;
	close;
}

// 3° Castelo
//============================================================
nguild_pay,101,25,0	script	Switch#DunN03	HIDDEN_NPC,{
	callfunc "F_GldDunSw","nguild_pay","01",186,165;
	close;
}

// 4° Castelo
//============================================================
nguild_prt,94,200,0	script	Switch#DunN04	HIDDEN_NPC,{
	callfunc "F_GldDunSw","nguild_prt","03",28,251;
	close;
}

// Gerenciadores
//==============================================
function	script	F_GldManager	{

	set @GID, GetCastleData(getarg(1),1);
	mes "["+getarg(0)+"]";
	if (@GID == 0){
		mes "Eu estou aguardando o meu mestre.";
		mes "Pois bem... Bravo aventureiro, siga o seu destino!";
		return;
	}
	if (getcharid(2) != @GID){
		mes "Eu estou aqui para seguir ordens do mestre(a) ^5533FF" + getguildmaster(@GID) + "^000000'!";
		mes "Hey! Você sequer faz parte do Clã!";
		return;
	}
	if (strcharinfo(0) != getguildmaster(@GID)){
		mes "Você não é ^5533FF" + getguildmaster(@GID) + "^000000!";
		mes "Eu sigo somente as ordens de ^5533FF" + getguildmaster(@GID) + "^000000'";
		return 0;
	}

	mes "Bem-vindo mestre ^5533FF" + getguildmaster(@GID) + "^000000!";
	mes "Dar-lhe-ei assistência no que puder!";
	next;

	menu "Serviço de Kafras.",M_Kaf, "Entrar na sala do tesouro.",M_Treas, "Cancelar.",M_End;

	//===========================
	M_Kaf:
	//======
		mes "[ "+getarg(0)+" ]";
		if (GetCastleData(getarg(1),9) == 1) goto L_Dismiss;
		if (getgdskilllv(@GID,10001) == 0){
			mes "Mestre, você não possui contrato com o Escritório Central das Kafras.";
			mes "Para contratar uma Kafra, você precisa possuir a habilidade de Clã ^5533FFContrato com Kafra^000000.";
			return;
		}

		L_Hire:
			mes "Você gostaria de contratar os serviços de uma Kafra? Você vai precisar de ^5533FF10.000 zenys^000000 para isto...";
			next;
			menu "Contratar Kafra.",-,"Cancelar.",sM_KafEnd;

				mes "["+getarg(0)+"]";
				if (Zeny < 10000){
					mes "Mestre, você não possui verba para contratar uma Kafra.";
					mes "A contratação foi cancelada.";
					return;
				}
				set Zeny,Zeny-10000;
				enablenpc "Assistente Kafra#"+getarg(4);
				SetCastleData getarg(1),9,1;
				mes "Você criou um contrato com o Escritório Central das Kafras.";
				next;
				cutin "kafra_01",2;
				mes "[Kafra]";
				mes "Como você está? Eu estou aqui para providenciar meus serviços úteis! Eu farei o melhor para servi-lo bem.";
				next;
				cutin "kafra_01",255;
				mes "["+getarg(0)+"]";
				mes "Acho que os serviços da Kafra irão beneficiar os membros do Clã.";
				return;

			sM_KafEnd:
				mes "["+getarg(0)+"]";
				mes "Como você quiser mestre, mas sugiro que contrate uma kafra novamente o mais rápido possível!";
				return;

		L_Dismiss:
			mes "Você gostaria de dispensar a Kafra atual?";
			next;
			menu "Dispensar.",-,"Cancelar.",sM_KafEnd2;

				cutin "kafra_01",2;
				mes "[Kafra]";
				mes "Oh, Mestre! Eu fiz algo errado? Se fiz, você poderia me perdoar?";
				next;
				menu "Dispensar a Kafra.",-,"Cancelar.",ssM_KafEnd2;

					mes "[Kafra]";
					mes "É uma pena que não irei mais servir ao seu Clã....";
					next;
					disablenpc "Assistente Kafra#"+getarg(4);
					SetCastleData getarg(1),9,0;
					cutin "kafra_01",255;
					mes "["+getarg(0)+"]";
					mes "A Kafra foi dispensada.";
					mes "Mas... Nós precisamos ter uma Kafra assim que possível!";
					return;
				ssM_KafEnd2:
					mes "[Kafra]";
					mes "Obrigado mestre! Eu farei o meu melhor!";
					cutin "kafra_01",255;
					return;
			sM_KafEnd2:
				mes "["+getarg(0)+"]";
				mes "Mestre, eu acho que é melhor manter a Kafra atual, pois ela está fazendo o seu melhor";
				return;

	//=========================
	M_Treas:
	//========
		mes "["+getarg(0)+"]";
		mes "Deseja ir para a sala do tesouro? Somente você, mestre do clã, pode entrar na sala do tesouro.";
		next;
		menu "Entrar na sala do tesouro.",-,"Cancelar.",sM_TresEnd;

			mes "["+getarg(0)+"]";
			mes "Por favor, siga-me até o caminho da passagem secreta.";
			mes "Você deve puxar a alavanca secreta para poder sair.";
			next;
			warp getarg(1),getarg(2),getarg(3);
			return;
		sM_TresEnd:
			mes "["+getarg(0)+"]";
			mes "Tesouros são produzidos todos os dias.";
			mes "Você deve recolher eles todos os dias ou eles podem desaparecer.";
			return;


	//==========================
	M_End:
	//=======
		mes "["+getarg(0)+"]";
		mes "Como você desejar mestre.";
		return;
}

// 1° Castelo ==================================================================================
nguild_alde,218,175,0	script	Frolo	1_M_JOBTESTER,{
	callfunc "F_GldManager","Frolo","nguild_alde",119,223,"N01";
	close;
}

// 2° Castelo ==================================================================================
nguild_gef,40,48,5	script	Leiber	1_M_JOBTESTER,{
	callfunc "F_GldManager","Leiber","nguild_gef",155,112,"N02";
	close;
}

// 3° Castelo ==================================================================================
nguild_pay,120,58,4	script	Dundar	1_M_JOBTESTER,{
	callfunc "F_GldManager","Dundar","nguild_pay",290,7,"N03";
	close;
}

// 4° Castelo ==================================================================================
nguild_prt,112,181,0	script	Thefton	1_M_JOBTESTER,{
	callfunc "F_GldManager","Thefton","nguild_prt",15,209,"N04";
	close;
}

// Kafras
function	script	F_GKafra	{
	cutin "kafra_01",2;
	set @GID, GetCastleData(getarg(0),1);
	if (getcharid(2) == @GID && getgdskilllv(@GID,10001)) goto L_StartG;

	mes "[Kafra]";
	mes "Eu estou instruída para oferecer meus serviços somente para o Clã ^ff0000" + GetGuildName(@GID) + "^000000.";
	mes "Por favor, procure outra Kafra na cidade mais próxima. Lamentamos pelo inconveniente.";
	cutin "",255;
	close;
L_StartG:
	set @wrpP[0], 200;
	set @wrpD$[0], getarg(1);
	setarray @wrpC$[0], @wrpD$[0]+" ^880000"+@wrpP[0]+"^000000 z", "Cancelar.", "", "", "","";
	callfunc "F_Kafra",2,0,0,0,800;
	end;
}

// 1° Castelo ===============================================
nguild_alde,218,170,0	script	Kafra#N01	4_F_KAFRA1,{
	callfunc "F_GKafra", "nguild_alde", "Prontera";
	end;
OnRecvCastleN01:
	if (GetCastleData("nguild_alde",9) < 1) disablenpc "Kafra#N01";
	end;
}

// 2° Castelo ===============================================
//nguild_gef,96,173,0	script	Kafra#N02	4_F_KAFRA1,{
nguild_gef,35,37,0	script	Kafra#N02	4_F_KAFRA1,{
	callfunc "F_GKafra", "nguild_gef", "Prontera";
	end;
OnRecvCastleN02:
	if (GetCastleData("nguild_gef",9) < 1) disablenpc "Kafra#N02";
	end;
}

// 3° Castelo ===============================================
nguild_pay,128,58,3	script	Kafra#N03	4_F_KAFRA1,{
	callfunc "F_GKafra", "nguild_pay", "Prontera";
	end;
OnRecvCastleN03:
	if (GetCastleData("nguild_pay",9) < 1) disablenpc "Kafra#N03";
	end;
}

// 4° Castelo ===============================================
nguild_prt,96,173,0	script	Kafra#N04	4_F_KAFRA1,{
	callfunc "F_GKafra", "nguild_prt", "Prontera";
	end;
OnRecvCastleN04:
	if (GetCastleData("nguild_prt",9) < 1) disablenpc "Kafra#N04";
	end;
}

// Portais
prontera,146,163,6	script	Castelos de Novatos	4_F_NOVICE,{
	mes "[Cita]";
	if (getcharid(2)!=0){
		if (getgdskilllv(getcharid(2),10013)){
			mes "Estou vendo que seu Clã tem a habilidade de 'Chamado Urgente' dominada.";
			mes "Você não pode entrar na área de castelo para Novatos.";
			emotion e_hmm;
			emotion e_wah,1;
			close;
		}
		if (
			getgdskilllv(getcharid(2),10000) +
			getgdskilllv(getcharid(2),10001) +
			getgdskilllv(getcharid(2),10002) +
			getgdskilllv(getcharid(2),10003) +
			getgdskilllv(getcharid(2),10004) +
			getgdskilllv(getcharid(2),10005) +
			getgdskilllv(getcharid(2),10006) +
			getgdskilllv(getcharid(2),10007) +
			getgdskilllv(getcharid(2),10008) +
			getgdskilllv(getcharid(2),10009) +
			getgdskilllv(getcharid(2),10010) +
			getgdskilllv(getcharid(2),10011) +
			getgdskilllv(getcharid(2),10012) +
			getgdskilllv(getcharid(2),10013) +
			getgdskilllv(getcharid(2),10014) > 9
		){
			mes "Estou vendo que seu Clã tem a habilidade de 'Chamado Urgente' dominada.";
			mes "Você não pode entrar na área de castelo para Novatos.";
			emotion e_hmm;
			emotion e_wah,1;
			close;
		}
	} else {
		mes "^FF0000Você precisa entrar em um Clã que seja capaz de acertar o Emperium!^000000";
	}
	mes "Eu sou o recepcionista para o castelo dos Novatos";
	next;
	if ( !(eaclass()&(EAJL_2|EAJL_UPPER)) && BaseLevel<60 )
		menu "Me teleporte para os castelos dos novatos.",M_WARP,"Cancelar.",-;

	mes "[Cita]";
	mes "Eu lamento, mas você não pode entrar no local sagrado dos castelos dos novatos.";
	emotion e_sry;
	close;

M_WARP:
	//Remoção de Buffs Inaptos aos NGuild
	sc_end SC_ASSUMPTIO;
	sc_end SC_IMPOSITIO;
	sc_end SC_SUFFRAGIUM;
	sc_end SC_MAGNIFICAT;
	sc_end SC_WEAPONPERFECT;
	sc_end SC_GOSPEL;
	sc_end SC_BASILICA;
	sc_end SC_MAGICPOWER;
	sc_end SC_MARIONETTE;
	sc_end SC_MARIONETTE2;
	sc_end SC_DEVOTION;
	sc_end SC_SACRIFICE;
	sc_end SC_OVERTHRUSTMAX;
	sc_end SC_SOULLINK;
	warp "n_castle",102,93+rand(14);
	close;
}

n_castle,102,107,5	script	Cita	4_F_NOVICE,{
	mes "[Cita]";
	mes "Olá, "+ strcharinfo(0) +".";
	mes "Como posso ajudar-lhe?";
	next;
	menu "Me teleporte para Prontera!",-,"Cancelar.",LEnd;

		warp "prontera",155,177+rand(5);
		close;
	LEnd:
		mes "[Cita]";
		mes "Está bem.";
		close;
}

//================================================
// Função de Spawn dos Tesouros
//================================================
function	script	F_GldTreas	{
	if (getarg(10) !=  1) {
		setcastledata getarg(0),4,0;
		setcastledata getarg(0),5,0;

		// Não spammar tesorous caso não tenha castelo ou a ECO do Clã esteja como 100.
		if (GetCastleData(getarg(0),2) > 100 || GetCastleData(getarg(0),1) == 0) return;

		if (compare(getarg(0),"nguild"))
			set getarg(2),1;
		else
			set getarg(2),GetCastleData(getarg(0),2)/5+4;

		if (getarg(2) <= 0) return;

		set getarg(3), getarg(2);
	}
	for (set .@i,1; .@i <= getarg(3) ; set .@i,.@i+1) {
		// Setar o ID do Baú do Tesouro.
		set getarg(4), getarg(5) + (.@i+1) % 2;
		areamonster getarg(0),getarg(6),getarg(7),getarg(8),getarg(9),"Baú do Tesouro",getarg(4),1,"Treasure_"+getarg(1)+"::OnDied";
	}
	return;
}

//================================================
//  Mudança para Sala do Tesouro
//================================================
function	script	F_GldTreasSw	{
	mes " ";
	mes "Há uma pequena alavanca aqui.";
	mes "Você deseja puxar ela?";
	next;

	if (select("Puxar.:Não puxar.") == 1) {
		warp getarg(0),getarg(1),getarg(2);
		return;
	}
	close;
}

// 1° Castelo
//================================================
// Spawn de Tesouros
//================================================
nguild_alde,1,1,1	script	Treasure_N01	-1,{

OnRecvCastleN01:
	end;
OnInit:
	if ($boxNumN01 == 0) end;
	set $@bxN01, $boxNumN01;
	callfunc "F_GldTreas","nguild_alde","N01",$boxNumN01,$@bxN01,$@boxIdN01,1324+rand(10),114,218,123,227,1;
	end;

OnDied:
	mapannounce "nguild_alde","O Baú do Tesouro foi aberto.",17;
	set $boxNumN01, $boxNumN01 -1;
	if ($boxNumN01 == 0) mapannounce "nguild_alde", "Todos os tesouros foram abertos. Você deve esperar até o próximo dia para abrir os tesouros novamente.",0;
	end;
}

// Mudança da Sala do Tesouro --------------------
nguild_alde,123,223,0	script	Switch#TresN01	HIDDEN_NPC,{
	callfunc "F_GldTreasSw", "nguild_alde",218,176;
	end;
}

// 2° Castelo
//================================================
// Spawn de Tesouros
//================================================
nguild_gef,1,1,1	script	Treasure_N02	-1,{

OnRecvCastleN02:
	end;
OnInit:
	if ($boxNumN02 == 0) end;
	set $@bxN02, $boxNumN02;
	callfunc "F_GldTreas","nguild_gef","N02",$boxNumN02,$@bxN02,$@boxIdN02,1334+rand(10),150,108,158,114,1;
	end;

OnDied:
	mapannounce "nguild_gef","O Baú do Tesouro foi aberto.",17;
	set $boxNumN02, $boxNumN02 -1;
	if ($boxNumN02 == 0) mapannounce "nguild_gef", "Todos os tesouros foram abertos. Você deve esperar até o próximo dia para abrir os tesouros novamente.",0;
	end;
}

// Mudança da Sala do Tesouro
//================================================
nguild_gef,152,117,0	script	Switch#TresN02	HIDDEN_NPC,{
	callfunc "F_GldTreasSw","nguild_gef",40,49;
	end;
}

// 3° Castelo
//================================================
// Spawn de Tesouros
//================================================
nguild_pay,1,1,0	script	Treasure_N03	-1,{
OnRecvCastleN03:
	end;
OnInit:
	if ($boxNumN03 == 0) end;
	set $@bxN03, $boxNumN03;
	callfunc "F_GldTreas","nguild_pay","N03",$boxNumN03,$@bxN03,$@boxIdN03,1344+rand(10),286,4,295,13,1;
	end;

OnDied:
	mapannounce "nguild_pay","O Baú do Tesouro foi aberto.",17;
	set $boxNumN03, $boxNumN03 -1;
	if ($boxNumN03 == 0) mapannounce "nguild_pay", "Todos os tesouros foram abertos. Você deve esperar até o próximo dia para abrir os tesouros novamente.",0;
	end;
}

// Mudança da Sala do Tesouro ---------------------------------------------------
nguild_pay,295,8,0	script	Switch#TresN03	HIDDEN_NPC,{
	callfunc "F_GldTreasSw", "nguild_pay",120,59;
	end;
}

// 4° Castelo
//================================================
// Spawn de Tesouros
//================================================
nguild_prt,1,1,0	script	Treasure_N04	-1,{
OnRecvCastleN04:
	end;
OnInit:
	if ($boxNumN04 == 0) end;
	set $@bxN04, $boxNumN04;
	callfunc "F_GldTreas","nguild_prt","N04",$boxNumN04,$@bxN04,$@boxIdN04,1354+rand(10),6,204,15,213,1;
	end;

OnDied:
	mapannounce "nguild_prt","O Baú do Tesouro foi aberto.",17;
	set $boxNumN04, $boxNumN04 -1;
	if ($boxNumN04 == 0) mapannounce "nguild_prt", "Todos os tesouros foram abertos. Você deve esperar até o próximo dia para abrir os tesouros novamente.",0;
	end;
}

// Mudança da Sala do Tesouro
//================================================
nguild_prt,15,209,0	script	Switch#TresN04	HIDDEN_NPC,{
	callfunc "F_GldTreasSw", "nguild_prt",109,179;
	end;
}

//------------------------------------------------------------------------------
nguild_alde,216,24,0	script	Guardião_N01	-1,{
OnAgitInit:
	if (GetCastleData("nguild_alde",10) == 1) guardian "nguild_alde",18,219,"Soldado Guardião",1287,"Guardião_N01::OnGuardiãoDerrotado",0;
	if (GetCastleData("nguild_alde",11) == 1) guardian "nguild_alde",117,42,"Soldado Guardião",1287,"Guardião_N01::OnGuardiãoDerrotado",1;
	if (GetCastleData("nguild_alde",12) == 1) guardian "nguild_alde",207,153,"Soldado Guardião",1287,"Guardião_N01::OnGuardiãoDerrotado",2;
	if (GetCastleData("nguild_alde",13) == 1) guardian "nguild_alde",68,70,"Arqueiro Guardião",1285,"Guardião_N01::OnGuardiãoDerrotado",3;
	if (GetCastleData("nguild_alde",14) == 1) guardian "nguild_alde",187,140,"Arqueiro Guardião",1285,"Guardião_N01::OnGuardiãoDerrotado",4;
	if (GetCastleData("nguild_alde",15) == 1) guardian "nguild_alde",62,204,"Cavaleiro Guardião",1286,"Guardião_N01::OnGuardiãoDerrotado",5;
	if (GetCastleData("nguild_alde",16) == 1) guardian "nguild_alde",113,100,"Cavaleiro Guardião",1286,"Guardião_N01::OnGuardiãoDerrotado",6;
	if (GetCastleData("nguild_alde",17) == 1) guardian "nguild_alde",211,174,"Cavaleiro Guardião",1286,"Guardião_N01::OnGuardiãoDerrotado",7;
	end;

OnGuardiãoDerrotado:
	// Evento quando o Guardião for derrotado
	MapAnnounce "nguild_alde","O Guardião foi derrotado",17;
	end;
}

//------------------------------------------------------------------------------
nguild_gef,198,182,0	script	Guardião_N02	-1,{
OnAgitInit:
	if (GetCastleData("nguild_gef",10) == 1) guardian "nguild_gef",30,178,"Soldado Guardião",1287,"Guardião_N02::OnGuardiãoDerrotado",0;
	if (GetCastleData("nguild_gef",11) == 1) guardian "nguild_gef",64,180,"Soldado Guardião",1287,"Guardião_N02::OnGuardiãoDerrotado",1;
	if (GetCastleData("nguild_gef",12) == 1) guardian "nguild_gef",61,25,"Soldado Guardião",1287,"Guardião_N02::OnGuardiãoDerrotado",2;
	if (GetCastleData("nguild_gef",13) == 1) guardian "nguild_gef",61,44,"Arqueiro Guardião",1285,"Guardião_N02::OnGuardiãoDerrotado",3;
	if (GetCastleData("nguild_gef",14) == 1) guardian "nguild_gef",189,43,"Arqueiro Guardião",1285,"Guardião_N02::OnGuardiãoDerrotado",4;
	if (GetCastleData("nguild_gef",15) == 1) guardian "nguild_gef",51,192,"Cavaleiro Guardião",1286,"Guardião_N02::OnGuardiãoDerrotado",5;
	if (GetCastleData("nguild_gef",16) == 1) guardian "nguild_gef",49,67,"Cavaleiro Guardião",1286,"Guardião_N02::OnGuardiãoDerrotado",6;
	if (GetCastleData("nguild_gef",17) == 1) guardian "nguild_gef",181,14,"Cavaleiro Guardião",1286,"Guardião_N02::OnGuardiãoDerrotado",7;
	end;

OnGuardiãoDerrotado:
	// Evento quando o Guardião for derrotado
	MapAnnounce "nguild_gef","O Guardião foi derrotado",17;
	end;
}
//------------------------------------------------------------------------------
nguild_pay,139,139,0	script	Guardião_N03	-1,{
OnAgitInit:
	if (GetCastleData("nguild_pay",10) == 1) guardian "nguild_pay",210,120,"Soldado Guardião",1287,"Guardião_N03::OnGuardiãoDerrotado",0;
	if (GetCastleData("nguild_pay",11) == 1) guardian "nguild_pay",69,26,"Soldado Guardião",1287,"Guardião_N03::OnGuardiãoDerrotado",1; 
	if (GetCastleData("nguild_pay",12) == 1) guardian "nguild_pay",23,141,"Soldado Guardião",1287,"Guardião_N03::OnGuardiãoDerrotado",2;
	if (GetCastleData("nguild_pay",13) == 1) guardian "nguild_pay",224,87,"Arqueiro Guardião",1285,"Guardião_N03::OnGuardiãoDerrotado",3;
	if (GetCastleData("nguild_pay",14) == 1) guardian "nguild_pay",81,45,"Arqueiro Guardião",1285,"Guardião_N03::OnGuardiãoDerrotado",4;
	if (GetCastleData("nguild_pay",15) == 1) guardian "nguild_pay",214,53,"Cavaleiro Guardião",1286,"Guardian_N03::OnGuardiãoDerrotado",5;
	if (GetCastleData("nguild_pay",16) == 1) guardian "nguild_pay",69,26,"Cavaleiro Guardião",1286,"Guardião_N03::OnGuardiãoDerrotado",6;
	if (GetCastleData("nguild_pay",17) == 1) guardian "nguild_pay",23,141,"Cavaleiro Guardião",1286,"Guardião_N03::OnGuardiãoDerrotado",7;
	end;

OnGuardiãoDerrotado:
	// Evento quando o Guardião for derrotado
	MapAnnounce "nguild_pay","O Guardião foi derrotado",17;
	end;
}
//------------------------------------------------------------------------------
nguild_prt,197,197,0	script	Guardião_N04	-1,{
OnAgitInit:
	if (GetCastleData("nguild_prt",10) == 1) guardian "nguild_prt",196,92,"Soldado Guardião",1287,"Guardião_N04::OnGuardiãoDerrotado",0;
	if (GetCastleData("nguild_prt",11) == 1) guardian "nguild_prt",113,200,"Soldado Guardião",1287,"Guardião_N04::OnGuardiãoDerrotado",1;
	if (GetCastleData("nguild_prt",12) == 1) guardian "nguild_prt",111,186,"Soldado Guardião",1287,"Guardião_N04::OnGuardiãoDerrotado",2;
	if (GetCastleData("nguild_prt",13) == 1) guardian "nguild_prt",76,202,"Arqueiro Guardião",1285,"Guardião_N04::OnGuardiãoDerrotado",3;
	if (GetCastleData("nguild_prt",14) == 1) guardian "nguild_prt",90,26,"Arqueiro Guardião",1285,"Guardião_N04::OnGuardiãoDerrotado",4;
	if (GetCastleData("nguild_prt",15) == 1) guardian "nguild_prt",58,59,"Cavaleiro Guardião",1286,"Guardião_N04::OnGuardiãoDerrotado",5;
	if (GetCastleData("nguild_prt",16) == 1) guardian "nguild_prt",112,200,"Cavaleiro Guardião",1286,"Guardião_N04::OnGuardiãoDerrotado",6;
	if (GetCastleData("nguild_prt",17) == 1) guardian "nguild_prt",101,194,"Cavaleiro Guardião",1286,"Guardião_N04::OnGuardiãoDerrotado",7;
	end;

OnGuardiãoDerrotado:
	// Evento quando o Guardião for derrotado
	MapAnnounce "nguild_prt","O Guardião foi derrotado",17;
	end;
}

// Função para OnAgitStart
//============================================================
function	script	F_AgitStart	{
	set .@map$, getarg(0);
	set .@castle$, getarg(1);
	set .@empx, getarg(2);
	set .@empy, getarg(3);

	MapRespawnGuildID .@map$,GetCastleData(.@map$,1),2;
	monster .@map$,.@empx,.@empy,"Emperium",1288,1,"Agit_"+.@castle$+"::OnAgitBreak";
	GvgOn .@map$;
	if (GetCastleData(.@map$,1) != 0) return;
	end;
}

// Função para OnGuildBreak
//============================================================
function	script	F_GuildBreak	{
	set .@map$, getarg(0);
	set .@castle$, getarg(1);

	killmonsterall .@map$;

	Announce "O castelo [" + GetCastleName(.@map$) + "] foi abandonado.",0;
	disablenpc "Assistente Kafra#"+.@castle$;
	SetCastleData .@map$,0,0;
	return;
}

// Função para OnAgitBreak
//============================================================
function	script	F_AgitBreak	{
	set .@map$, getarg(0);
	set .@castle$, getarg(1);

	set .@GID,getcharid(2);
	if (.@GID <= 0) return;

	set .@Economy,GetCastleData(.@map$,2) - 5;
	if (.@Economy < 0) set .@Economy, 0;
	SetCastleData .@map$, 2, .@Economy;

	set .@Defence,GetCastleData(.@map$,3) - 5;
	if (.@Defence < 0) set .@Defence, 0;
	SetCastleData .@map$, 3, .@Defence;

	SetCastleData .@map$,1, .@GID;
	MapAnnounce .@map$,"O Emperium foi destruído!",bc_map,0x00CCFF;
	Announce "O castelo [" + GetCastleName(.@map$) + "] foi conquistado pelo Clã [" + GetGuildName(.@GID) + "].",bc_all;
	GetCastleData .@map$,0,"::OnRecvCastle"+.@castle$;

	disablenpc "Assistente Kafra#"+.@castle$;

	// Removendo dados de investimento e kafra
	for( set .@i, 4; .@i <= 9; set .@i, .@i+1 )
		SetCastleData .@map$, .@i, 0;

	// Se a guilda não tiver habilidades para usod e guardiões, removê-los
	if ( getgdskilllv(.@GID,10002) == 0 )
		for( set .@i, 10; .@i <= 17; set .@i, .@i+1 )
			SetCastleData .@map$, .@i, 0;

	return;
}

// Função para OnAgitEnd
//============================================================
function	script	F_AgitEnd	{
	set .@map$, getarg(0);
	set .@castle$, getarg(1);

	GvgOff .@map$;
	if (GetCastleData(.@map$,1) == 0) return;
	MapRespawnGuildID .@map$,GetCastleData(.@map$,1),4;
	KillMonster .@map$,"Agit_"+.@castle$+"::OnAgitBreak";
	end;
}

// 1° Castelo
//============================================================
nguild_alde,0,0,0	script	Agit_N01	-1,{
OnInterIfInitOnce:
	GetCastleData "nguild_alde",0,"::OnRecvCastleN01";
	end;
OnRecvCastleN01:
	RequestGuildInfo GetCastleData("nguild_alde",1);
	end;
OnAgitStart:
	callfunc "F_AgitStart","nguild_alde","N01",216,24;
	end;
OnAgitBreak:
	callfunc "F_AgitBreak","nguild_alde","N01";
	goto OnAgitEliminate;
	end;
OnGuildBreak:
	callfunc "F_GuildBreak","nguild_alde","N01";
	end;
OnAgitEliminate:
	MapRespawnGuildID "nguild_alde",GetCastleData("nguild_alde",1),6;
	Monster "nguild_alde",216,24,"EMPERIUM",1288,1,"Agit_N01::OnAgitBreak";
	end;
OnAgitEnd:
	callfunc "F_AgitEnd","nguild_alde","N01";
	end;
}

// 2° Castelo
//============================================================
nguild_gef,0,0,0	script	Agit_N02	-1,{
OnInterIfInitOnce:
	GetCastleData "nguild_gef",0,"::OnRecvCastleN02";
	end;
OnRecvCastleN02:
	RequestGuildInfo GetCastleData("nguild_gef",1);
	end;
OnAgitStart:
	callfunc "F_AgitStart","nguild_gef","N02",198,182;
	end;
OnAgitBreak:
	callfunc "F_AgitBreak","nguild_gef","N02";
	goto OnAgitEliminate;
	end;
OnGuildBreak:
	callfunc "F_GuildBreak","nguild_gef","N02";
	end;
OnAgitEliminate:
	MapRespawnGuildID "nguild_gef",GetCastleData("nguild_gef",1),6;
	Monster "nguild_gef",198,182,"EMPERIUM",1288,1,"Agit_N02::OnAgitBreak";
	end;
OnAgitEnd:
	callfunc "F_AgitEnd","nguild_gef","N02";
	end;
}

// 3° Castelo
//============================================================
nguild_pay,0,0,0	script	Agit_N03	-1,{
OnInterIfInitOnce:
	GetCastleData "nguild_pay",0,"::OnRecvCastleN03";
	end;
OnRecvCastleN03:
	RequestGuildInfo GetCastleData("nguild_pay",1);
	end;
OnAgitStart:
	callfunc "F_AgitStart","nguild_pay","N03",139,139;
	end;
OnAgitBreak:
	callfunc "F_AgitBreak","nguild_pay","N03";
	goto OnAgitEliminate;
	end;
OnGuildBreak:
	callfunc "F_GuildBreak","nguild_pay","N03";
	end;
OnAgitEliminate:
	MapRespawnGuildID "nguild_pay",GetCastleData("nguild_pay",1),6;
	Monster "nguild_pay",139,139,"EMPERIUM",1288,1,"Agit_N03::OnAgitBreak";
	end;
OnAgitEnd:
	callfunc "F_AgitEnd","nguild_pay","N03";
	end;
}

// 4° Castelo
//============================================================
nguild_prt,0,0,0	script	Agit_N04	-1,{
OnInterIfInitOnce:
	GetCastleData "nguild_prt",0,"::OnRecvCastleN04";
	end;
OnRecvCastleN04:
	RequestGuildInfo GetCastleData("nguild_prt",1);
	end;
OnAgitStart:
	callfunc "F_AgitStart","nguild_prt","N04",197,197;
	end;
OnAgitBreak:
	callfunc "F_AgitBreak","nguild_prt","N04";
	goto OnAgitEliminate;
	end;
OnGuildBreak:
	callfunc "F_GuildBreak","nguild_prt","N04";
	end;
OnAgitEliminate:
	MapRespawnGuildID "nguild_prt",GetCastleData("nguild_prt",1),6;
	Monster "nguild_prt",197,197,"EMPERIUM",1288,1,"Agit_N04::OnAgitBreak";
	end;
OnAgitEnd:
	callfunc "F_AgitEnd","nguild_prt","N04";
	end;
}

// Tempo de Spawn dos Tesouros
//============================================================
-	script	TreasSpawn	-1,{
	end;

OnClock0005:
	callfunc "F_GldTreas","nguild_alde","N01",$boxNumN01,$@bxN01,$@boxIdN01,1324,114,218,123,227,0;
	callfunc "F_GldTreas","nguild_gef","N02",$boxNumN02,$@bxN02,$@boxIdN02,1334,150,108,158,114,0;
	callfunc "F_GldTreas","nguild_pay","N03",$boxNumN03,$@bxN03,$@boxIdN03,1344,286,4,295,13,0;
	callfunc "F_GldTreas","nguild_prt","N04",$boxNumN04,$@bxN04,$@boxIdN04,1354,6,204,15,213,0;
	end;
}