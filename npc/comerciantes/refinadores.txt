//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: refinadores.txt                        |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 3.1/3.2a/1.1                        |
//   |--------------------------------------------------------|
//   | Descrição: Refinadores e refinadores especiais.        |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Shiraz]                          |
//   | 1.1 Adequado ao Modo Old-Times [Holy & Neko]           |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

// Christopher: Geffen Blacksmith
//============================================================
geffen_in,110,172,0	script	Christopher#1	1_M_SMITH,{
	mes "[Christopher Guillenrow]";
	mes "Welcome to Christopher's Workshop. Ye can get all yer stuff for forging here. What business";
	mes "brings ye to me?";
	next;
	switch(select("Purchase Anvil:Purchase Forging Item:Purchase Metal:Purify Rough Ores:Cancel")) {
	case 1:
		mes "[Christopher Guillenrow]";
		mes "A better Anvil gives ye a greeeater chance to make better weapons, ye know? But they'll cost ye more zeny. Just get it off yer chest and buy what fits your purposes best, laddy.";
		next;
		switch(select("Anvil - 30,000 zeny:Oridecon Anvil - 120,000 zeny:Golden Anvil - 300,000 zeny:Better Anvil than the others.:Cancel.")) {
		case 1:
			if (Zeny < 30000) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			getitem 986,1; // Anvil
			set Zeny, Zeny-30000;
			mes "[Christopher Guillenrow]";
			mes "This is the cheapest one, but efficient enough to forge most items. Thank ye fer shopping at me workshop.  Feel free to come anytime, whenever ye need.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			getitem 987,1; // Oridecon_Anvil
			set Zeny, Zeny-120000;
			mes "[Christopher Guillenrow]";
			mes "Aye, friend ye have an eye for the anvil. This must be the proper anvil for a Blacksmith, eh? Thank ye fer shopping at me workshop.  Feel free to come anytime, whenever ye need.";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			getitem 988,1; // Golden_Anvil
			set Zeny, Zeny-300000;
			mes "[Christopher Guillenrow]";
			mes "This one is the best among all me stuffs in me workshop! With this, ye can rule the Blacksmith world! Thank ye fer shopping at me workshop.  Feel free to come anytime, whenever ye need.";
			close;
		case 4:
			mes "[Christopher Guillenrow]";
			mes "Well, sorry. But I don't have anythin' harder' than the Golden Anvil.";
			next;
			mes "[Christopher Guillenrow]";
			mes "Me thinks 'Ringgel,' the Legendary Anvil Maker would have one. But, I don't think ye can find him, though he be somewhere in this world.";
			close;
		case 5:
			mes "[Christopher Guillenrow]";
			mes "Okay, feel free to come anytime, whenever ye need. Fare ye well.";
			close;
		}
	case 2:
		mes "[Christopher Guillenrow]";
		mes "A respectable blacksmith uses fine tools. Ye can become one o'those with me Stuff. Choose anything ye want.";
		next;
		switch(select("Mini-Furnace - 150 zeny:Iron Hammer - 1000 zeny:Golden Hammer - 3000 zeny:Oridecon Hammer - 5000 zeny:Cancel.")) {
		case 1:
			mes "[Christopher Guillenrow]";
			mes "It's a much needed tool fer refining metal! So, How many do ye wish to buy? If ye want to quit, just type the number '0.'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "Aye, the deal is canceled. Fare ye well.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "Ye can buy 500, er less.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 150;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			if (checkweight(612,.@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "Ye look like you don't got enough room in yer inventory. Put some stuff into your Kafra Storage, why don't ye?";
				close;
			}
			getitem 612,.@input; // Portable_Furnace
			set Zeny, Zeny-.@sell;
			mes "[Christopher Guillenrow]";
			mes "Thank ye fer shopping at me workshop. Feel free to come anytime, whenever ye need.";
			close;
		case 2:
			if (Zeny < 1000) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			getitem 613,1; // Iron_Hammer
			set Zeny, Zeny-1000;
			mes "[Christopher Guillenrow]";
			mes "Thank ye fer shopping at me workshop. Feel free to come anytime, whenever ye need.";
			close;
		case 3:
			if (Zeny < 3000) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			getitem 614,1; // Golden_Hammer
			set Zeny, Zeny-3000;
			mes "[Christopher Guillenrow]";
			mes "Thank ye fer shopping at me workshop. Feel free to come anytime, whenever ye need.";
			close;
		case 4:
			if (Zeny < 5000) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			getitem 615,1; // Oridecon_Hammer
			set Zeny, Zeny-5000;
			mes "[Christopher Guillenrow]";
			mes "Thank ye fer shopping at me workshop. Feel free to come anytime, whenever ye need.";
			close;
		case 5:
			mes "[Christopher Guillenrow]";
			mes "Feel free to come anytime, whenever ye need. Fare ye well.";
			close;
		}
	case 3:
		mes "[Christopher Guillenrow]";
		mes "I prepare every Metal, and only the high quality ones o'course. Now then, which one do ye need?";
		next;
		switch(select("Phracon - 200z.:Emveretarcon - 1000z.:Cancel.")) {
		case 1:
			mes "[Christopher Guillenrow]";
			mes "So, How many do ye wish to buy? If ye dont want anything, just type the number as '0.'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "Deal has";
					mes "been canceled.";
					mes "Fare ye well.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "Ye can buy 500, er less.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 200;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "Ye don't have enough money. Ye know I can't sell this at a lower price... You know how the wifey nags about Zeny.";
				close;
			}
			if (checkweight(1010,.@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "Ye look like you don't have the room to carry anythin' new. Why don't ye put some things into Kafra Storage n' come back.";
				close;
			}
			getitem 1010,.@input; // Phracon
			set Zeny, Zeny-.@sell;
			mes "[Christopher Guillenrow]";
			mes "Thank ye fer shopping at me workshop. Feel free to come anytime, whenever ye need.";
			close;
		case 2:
			mes "[Christopher Guillenrow]";
			mes "So, how many do ye wish to buy? If ye dont want anything at all, just type the number as '0.'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "Deal has";
					mes "been canceled.";
					mes "Fare ye well.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "Ye can buy 500, er less.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 1000;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			if (checkweight(1011,.@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "Me friend... Seems to me ye don't have Inventory space. Why doncha put some things into Kafra Storage first?";
				close;
			}
			getitem 1011,.@input; // Emveretarcon
			set Zeny, Zeny-.@sell;
			mes "[Christopher Guillenrow]";
			mes "Thank ye fer shopping at me workshop. Feel free to come anytime, whenever ye need, whenever ye want.";
			close;
		case 3:
			mes "[Christopher Guillenrow]";
			mes "Feel free to come anytime, whenever ye need. Fare ye well.";
			close;
		}
	case 4:
		mes "[Christopher Guillenrow]";
		mes "I can purify yer Oridecon and Elunium. I make a refined Ore out of 5 o'each rough ones. Well... Which one do ye want to make?";
		next;
		switch(select("Make Oridecon:Make Elunium:Cancel.")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[Christopher Guillenrow]";
				mes "I told ye, I need 5 o'the rough Oridecons fer one Oridecon.";
				close;
			}
			else {
				delitem 756,5;  //Oridecon_Stone
				getitem 984,1; // Oridecon
				mes "[Christopher Guillenrow]";
				mes "Here's an Oridecon fer ye. Ye will be always welcome here, I'll be waitin' for ye.";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[Christopher Guillenrow]";
				mes "I told ye, I need 5 rough Eluniums fer one Elunium.";
				close;
			}
			else {
				delitem 757,5;  //Elunium_Stone
				getitem 985,1; // Elunium
				mes "[Christopher Guillenrow]";
				mes "Arrr, here's yer Elunium. Yer business is always welcome here, so feel free to come again.";
				close;
			}
		case 3:
			mes "[Christopher Guillenrow]";
			mes "Feel free to come anytime, whenever ye need. Fare ye well.";
			close;
		}
	case 5:
		mes "[Christopher Guillenrow]";
		mes "Feel free to come anytime, whenever ye need and whenever ye want. Fare ye well.";
		close;
	}
}

// Weapon/Armor Refiners
//============================================================
prt_in,63,60,0	script	Hollgrehenn	4_M_03,{
	callfunc "refinemain",strnpcinfo(0),0;
	end;
}

morocc_in,73,38,6	duplicate(Hollgrehenn)	Aragham	4W_M_03
alberta_in,28,58,0	duplicate(Hollgrehenn)	Fredrik	4_M_03
yuno_in01,171,21,4	duplicate(Hollgrehenn)	Lambert	4_M_ORIENT01
ein_in01,24,87,5	duplicata(Hollgrehenn)	Manthasman	4_M_DWARF
lhz_in02,282,20,7	duplicata(Hollgrehenn)	Fulerr	4_M_LGTMAN
payon,144,173,5	duplicata(Hollgrehenn)	Antonio	4_M_ORIENT01,{}
payon_in01,90,31,4	duplicato(Hollgrehenn)	Antonio#ot	4_M_ORIENT01,{}

//============================================================
//= Main Refiner Function
//============================================================
//= To allow auto safe refining/multiple refining set the
//= second argument to '1' in the function call.
//= If you enable this function, be sure to edit the value of
//= .@safe to the max safe refine in refine_db.txt as well.
//============================================================
function	script	refinemain	{
	disable_items;
	set .@features,getarg(1);
	mes "[" + getarg(0) + "]";
	mes "I'm the Armsmith.";
	mes "I can refine all kinds of weapons, armor and equipment, so let me";
	mes "know what you want me to refine.";
	next;

	setarray .@position$[1], "Head","Body","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2","Head 2","Head 3";
	set .@menu$,"";
	for(set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@i))
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";
		set .@menu$, .@menu$ + ":";
	}
	set .@part,select(.@menu$);
	if (!getequipisequiped(.@part)) {
		mes "[" + getarg(0) + "]";
		mes "You're not wearing";
		mes "anything there that";
		mes "I can refine.";
		emotion e_an;
		close;
	}
	//Check if the item is refinable...
	if (!getequipisenableref(.@part)) {
		mes "[" + getarg(0) + "]";
		mes "I don't think I can";
		mes "refine this item at all...";
		close;
	}
	//Check to see if the items is already +10
	if (getequiprefinerycnt(.@part) >= 10) {
		mes "[" + getarg(0) + "]";
		mes "I can't refine this";
		mes "any more. This is as";
		mes "refined as it gets!";
		close;
	}
	set .@refineitemid, getequipid(.@part); // save id of the item
	set .@refinerycnt, getequiprefinerycnt(.@part); //save refinery count
	switch(getequipweaponlv(.@part)){
	case 0: 	//Refine Armor
		set .@price,2000;
		set .@material,985;
		set .@safe,4;
		break;
	case 1: 	//Refine Level 1 Weapon
		set .@price,50;
		set .@material,1010;
		set .@safe,7; 
		break;
	case 2: 	//Refine Level 2 Weapon
		set .@price,200;
		set .@material,1011;
		set .@safe,6; 
		break;
	case 3: 	//Refine Level 3 Weapon
		set .@price,5000;
		set .@material,984;
		set .@safe,5; 
		break;
	case 4: 	//Refine Level 4 Weapon
		set .@price,20000;
		set .@material,984;
		set .@safe,4; 
		break;
	case 5: 	//Refine other stuff?
		set .@price,2000;
		set .@material,985;
		set .@safe,4; 
		break;
	}
	if (.@features != 1) {
		mes "[" + getarg(0) + "]";
		mes "To refine this I need";
		mes "one ^003366"+getitemname(.@material)+"^000000 and";
		mes "a service fee of " + .@price + " Zeny.";
		mes "Do you really wish to continue?";
		next;
		if (select("Yes:No") == 2){
			mes "[" + getarg(0) + "]";
			mes "Yeah...";
			mes "There's no need to";
			mes "rush. Take your time.";
			close;
		}
		if (getequippercentrefinery(.@part) < 100) {
			mes "[" + getarg(0) + "]";
			mes "Oh no! If I continue to";
			mes "refine this, there's a risk it could";
			switch(.@material) {
			case 985:
				mes "be destroyed! That means that ^FF0000this equipment^000000, and ^FF0000any cards^000000 or special properties added to this armor, ^FF0000will be gone^000000.";
				break;
			default:
				mes "be destroyed, and you'd ^FF0000lose the weapon^000000, any ^FF0000cards in the weapon^000000,";
				mes "or any added special properties.";
				break;
			}
			next;
			mes "["+getarg(0)+"]";
			mes "I can't make it any clearer.";
			mes "Once a weapon is destroyed,";
			mes "there's no getting it back.";
			mes "You really have a chance to";
			mes "^FF0000lose this weapon^000000 forever.";
			mes "Do you still want to refine?";
			next;
			if (select("Yes:No") == 2){
				mes "[" + getarg(0) + "]";
				mes "I completely agree...";
				mes "I might be a great refiner, but sometimes even I make mistakes.";
				close;
			}
		}
		if ((countitem(.@material) < 1) || (Zeny < .@price)) {
			mes "[" + getarg(0) + "]";
			mes "You don't seem to have";
			mes "enough Zeny or "+getitemname(.@material)+"...";
			mes "Go get some more. I'll be";
			mes "here all day if you need me.";
			close;
		}
		set Zeny,Zeny-.@price;
		delitem .@material,1;
		if (getequipisequiped(.@part) == 0) { // hacker has removed the item (not changed, why?)
			mes "[" + getarg(0) + "]";
			mes "Look here... you don't have any Items on...";
			close;
		}
		if (getequiprefinerycnt(.@part) != .@refinerycnt || getequipid(.@part) != .@refineitemid) { // hacker has changed the item
			mes "[" + getarg(0) + "]";
			emotion e_an;
			mes "Wait a second...";
			mes "Do you think I'm stupid?!";
			mes "You switched the item while I wasn't looking! Get out of here!";
			close;
		}
		if (getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			mes "[" + getarg(0) + "]";
			emotion (!rand(5))?e_cash:e_omg;
			set .@lose,rand(1,3);
			if (.@lose == 1) {
				mes "OH! MY GOD!";
				mes "Damn it! Not again!";
				mes "I'm terribly sorry, but you know practice does make perfect.";
				mes "Um, right? Heh heh...";
			} else if (.@lose == 2) {
				mes "Nooooooo!";
				mes "It broke!";
				mes "I-I'm sorry!";
			} else {
				mes "Crap!";
				mes "It couldn't take";
				mes "much more tempering!";
				mes "Sorry about this...";
			}
			close;
		}
		mes "["+getarg(0)+"]";
		successrefitem .@part;
		emotion e_heh;
		set .@win,rand(1,3);
		if (.@win == 1) {
			mes "Perfect!";
			mes "Heh heh!";
			mes "Once again,";
			mes "flawless work";
			mes "from the master~";
		} else if (.@win == 2) {
			mes "Success...!";
			mes "Yet again, my amazing";
			mes "talent truly dazzles";
			mes "and shines today.";
		} else {
			mes "Heh heh!";
			mes "I'm all done.";
			mes "No doubt, my work is";
			mes "to your satisfaction.";
			mes "Sheer, utter perfection~";
		}
		close;
	}

// New Refining Functions ========================
	if (getequiprefinerycnt(.@part) < .@safe) {
		mes "[" + getarg(0) + "]";
		mes "I can refine this to the safe limit or a desired number of times. It's your choice.";
		next;
		set .@menu2,select("To the safe limit, please.","I'll decide how many times.","I've changed my mind...");
	} else
		set .@menu2,2;
	switch(.@menu2){
	case 1: 
		set .@refinecnt,.@safe - getequiprefinerycnt(.@part);
		break;
	case 2:
		next;
		mes "[" + getarg(0) + "]";
		mes "How many times would you like me to refine your item?";
		next;
		input .@refinecnt;
		set .@refinecheck,.@refinecnt + getequiprefinerycnt(.@part);
		if (.@refinecnt < 1 || .@refinecheck > 10) {
			mes "[" + getarg(0) + "]";
			mes "I can't refine this item that many times.";
			close;
		}
		if (.@refinecheck > .@safe) {
			set .@refinecheck,.@refinecheck - .@safe;
			mes "[" + getarg(0) + "]";
			mes "This will try to refine the equipment " + .@refinecheck + " times past the safe limit. Your equipment may be destroyed... is that ok?";
			next;
			if (select("Yes...","No...") == 2){
				mes "[" + getarg(0) + "]";
				mes "You said so... So be it.";
				close;
			}
		}
		break;
	case 3:
		next;
		mes "[" + getarg(0) + "]";
		mes "You said so... So be it.";
		close;
	}
	set .@fullprice,.@price * .@refinecnt;
	mes "[" + getarg(0) + "]";
	mes "That will cost you " + .@refinecnt + " " + getitemname(.@material) + " and " + .@fullprice + " Zeny. Is that ok?";
	next;
	if (select("Yes","No...") == 2){
		mes "[" + getarg(0) + "]";
		mes "You said so... So be it.";
		close;
	}
	if (countitem(.@material) < .@refinecnt || Zeny < .@fullprice) {
		mes "[" + getarg(0) + "]";
		mes "Is that all you got? Unfortunately I can't work for you at a lower price. Try putting yourself in my shoes.";
		close;
	}
	set Zeny,Zeny - .@fullprice;
	delitem .@material,.@refinecnt;
	while(.@refinecnt){
		if (getequipisequiped(.@part) == 0) {
			mes "[" + getarg(0) + "]";
			mes "Look here... you don't have any items on...";
			close;
		}
		if (getequipid(.@part) != .@refineitemid || (.@menu2 == 1 && getequippercentrefinery(.@part) < 100)) {
			mes "[" + getarg(0) + "]";
			mes "Clang... No, but did you imagine I could be so stupid?!";
			mes "You changed it...";
			mes "Get out before I stun you with my Hammer!!";
			close;
		} 
		mes "Clang, clang!!!";
		if (.@menu2 == 2 && getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			emotion e_omg;
			mes "[" + getarg(0) + "]";
			mes "WAHHHH!!! I'm so sorry... I warned you this could happen...";
			set .@refinecnt,.@refinecnt - 1;
			if (.@refinecnt == 0) close;
			mes "Here's the unused Zeny and materials back...";
			getitem .@material,.@refinecnt;
			set .@fullprice,.@refinecnt * .@price;
			set Zeny,Zeny + .@fullprice;
			close;
		}
		successrefitem .@part;
		emotion e_no1;
		set .@refinecnt,.@refinecnt - 1;
		next;
	}
	mes "[" + getarg(0) + "]";
	mes "All finished... Come again soon.";
	close;
}

// Material Salesmen
//============================================================
prt_in,56,68,5	script	Vurewell	4_M_04,{
	callfunc "phramain",strnpcinfo(0);
	end;
}

morocc_in,63,32,6	duplicate(Vurewell)	Sade	4W_M_03
alberta_in,13,71,3	duplicate(Vurewell)	Kahlamanlith	4_M_04
yuno_in01,171,27,4	duplicate(Vurewell)	Dilemma	4_M_ORIENT01
payon,145,178,3	duplicata(Vurewell)	Begnahd	4_M_ORIENT01
ein_in01,15,87,3	duplicata(Vurewell)	Tirehaus	4_M_04
lhz_in02,278,24,3	duplicata(Vurewell)	Krugg	4_M_04,{}
payon_in01,84,26,4	duplicato(Vurewell)	Begnahd	4_M_ORIENT01,{}

// Material Salesmen Functions
//============================================================
function	script	phramain	{
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	mes "[" + getarg(0) + "]";
	mes "I sell 2 kinds of Metal";
	mes "for tempering weaponry.";
	mes "I have ^007777Phracon^000000 for Level 1";
	mes "Weapons, and ^007777Emveretarcon^000000";
	mes "for Level 2 Weapons.";
	next;
	switch(select("Phracon - 200 Zeny:Emveretarcon - 1000 Zeny:Ask about other Metals")) {
	case 1:
		set .@material,1010;
		set .@price,200;
		break;
	case 2:
		set .@material,1011;
		set .@price,1000;
		break;
	case 3:
		mes "[" + getarg(0) + "]";
		mes "Other metals?";
		mes "Well, you'll need special metals to upgrade higher level weapons, or any kind of armor. But you know, Oridecon and Elunium is really";
		mes "hard to just find...";
		close;
	}
	mes "[" + getarg(0) + "]";
	mes "So how many do you wish to buy?";
	mes "If you don't want any, please enter the number, '0.'";
	next;
	while(1) {
		input .@input;
		if (.@input == 0) {
			mes "[" + getarg(0) + "]";
			mes "The deal has";
			mes "been cancelled.";
			close;
		}
		else if (.@input < 0 || .@input > 500) {
			mes "[" + getarg(0) + "]";
			mes "Alright, you can";
			mes "puchase up to 500.";
			mes "No more than that,";
			mes "got it? Good.";
			next;
		}
		else {
			break;
		}
	}
	set .@sell,.@input * .@price;
	if (Zeny < .@sell) {
		mes "[" + getarg(0) + "]";
		mes "Err...";
		mes "You don't have";
		mes "enough Zeny to buy";
		mes ""+ .@input +" of them.";
		close;
	}
	if (checkweight(.@material,.@input) == 0) {
		mes "[" + getarg(0) + "]";
		mes "Hmm...";
		mes "I can't give you anything if you don't have enough room in your inventory. Why don't you put your extra things in Kafra Storage and try again?";
		close;
	}
	getitem .@material,.@input;
	set Zeny,Zeny-.@sell;
	mes "[" + getarg(0) + "]";
	mes "Here you are!";
	mes "Thank you for";
	mes "your patronage.";
	close;
}

// Ori/Elu Refiners
//============================================================
prt_in,63,69,3	script	Dietrich	4_M_02,{
	callfunc "orimain",strnpcinfo(0);
	end;
}

morocc_in,72,32,6	duplicate(Dietrich)	Abdula	4W_M_03
alberta_in,21,63,5	duplicate(Dietrich)	Xenophon	4_M_02
yuno_in01,164,27,4	duplicate(Dietrich)	Delight	4_M_ORIENT01
payon,137,178,5	duplicata(Dietrich)	Hakhim	4_M_ORIENT01
ein_in01,18,82,6	duplicata(Dietrich)	Matestein	4_M_02
lhz_in02,281,24,5	duplicata(Dietrich)	Fruel	4_M_02,{}
payon_in01,81,31,4	duplicato(Dietrich)	Hakhim#ot	4_M_ORIENT01,{}

// Ori/Elu Functions
//============================================================
function	script	orimain	{
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	mes "[" + getarg(0) + "]";
	mes "I can purify your";
	mes "Rough Oridecons or";
	mes "Rough Eluniums. I'll need";
	mes "5 Rough Stones to make";
	mes "1 pure one for you.";
	next;
	switch(select("Make Oridecon:Make Elunium:Ask about Enchanted Stones")) {
	case 1:
		if (countitem(756) > 4) {
			delitem 756,5;  //Oridecon_Stone
			getitem 984,1; // Oridecon
			mes "[" + getarg(0) + "]";
			mes "Here's your Oridecon.";
			mes "You're welcome to come";
			mes "back whenever you want.";
			close;
		}
		else {
			mes "[" + getarg(0) + "]";
			mes "You're kidding me, right?";
			mes "I just told you that I need 5 Rough Oridecons to make a pure Oridecon.";
			close;
		}
	case 2:
		if (countitem(757) > 4) {
			delitem 757,5;  //Elunium_Stone
			getitem 985,1; // Elunium
			mes "[" + getarg(0) + "]";
			mes "Here's your Elunium.";
			mes "You're welcome to come";
			mes "back whenever you want.";
			close;
		}
		else {
			mes "[" + getarg(0) + "]";
			mes "You're kidding me, right?";
			mes "I just told you that I need 5 Rough Eluniums to make a pure Elunium.";
			close;
		}
	case 3:
		mes "[" + getarg(0) + "]";
		mes "Enchanted Stones...?";
		mes "I've been a stonesmith for 20 years, so I've heard a lot about them. Supposedly, there are";
		mes "four different kinds.";
		next;
		mes "[" + getarg(0) + "]";
		mes "Each Enchanted Stone possesses one of the following elemental properties: Earth, Wind, Water and Fire.";
		next;
		mes "[" + getarg(0) + "]";
		mes "If someone combines a Enchanted Stone with a weapon while smithing, that weapon will possess the same property as the Stone.";
		next;
		mes "[" + getarg(0) + "]";
		mes "Needless to say, you need to have some smithing skill to produce this kind of elemental weapon.";
		close;
	}
}

// Equipment Repairmen
//============================================================
alberta_in,31,65,4	script	Repairman#alb	4_M_04,{
	callfunc "repairmain",((strnpcinfo(4) == "prt_in") ? "Grendal":"Repairman");
	end;
}

prt_in,63,54,2	duplicate(Repairman#alb)	Repairman#prt	4_M_04
yuno_in01,175,28,3	duplicate(Repairman#alb)	Repairman#juno	4_M_04
geffen_in,34,166,3	duplicate(Repairman#alb)	Repairman#gef	4W_M_03,
aldeba_in,38,60,3	duplicate(Repairman#alb)	Repairman#alde	4_M_04
prt_gld,139,117,4	duplicata(Repairman#alb)	Repairman#prt_gld	4_M_04
gef_fild13,263,117,4	duplicata(Repairman#alb)	Repairman#gef_fild	4_M_04
pay_gld,295,183,4	duplicata(Repairman#alb)	Repairman#pay_gld	4_M_04
alde_gld,220,152,4	duplicata(Repairman#alb)	Repairman#alde_gld	4_M_04
moc_ruins,107,94,4	duplicata(Repairman#alb)	Repairman#moc	4W_M_03
lhz_in02,284,14,3	duplicata(Repairman#alb)	Repairman#lhz	4_M_04
aru_gld,189,336,4	duplicata(Repairman#alb)	Repairman#aru_gld	4_M_04
sch_gld,340,80,7	duplicata(Repairman#alb)	Repairman#sch_gld	4_M_04,{}

// Equipment Repair Function
//============================================================
function	script	repairmain	{
		
	set .@repairprice,5000;
	mes "["+getarg(0)+"]";
	mes "Ei você!";
	mes "Você precisa que eu repare algum item?";
	mes "Você pode contar comigo para reparar itens!";
	next;
	if (select("Atualmente, eu tenho alguns...:Nenhum no momento.") == 1) {
		set .@checkitem,1;
		while (1) {
			if (getbrokenid(.@checkitem) == 0) {
				break;
			}
			set .@checkitem,.@checkitem+1;
		}
		set .@checkitem,.@checkitem-1;
		if (!.@checkitem) {
			mes "["+getarg(0)+"]";
			mes "Oh, isso ? incrivel!";
			mes "Parece que você toma muito cuidado com as suas coisas. Nenhum dos seus itens foi danificado!";
			next;
			mes "["+getarg(0)+"]";
			mes "Se todos fossem como você, eu estaria desempregado!! Haha~!";
			close;
		}
		mes "["+getarg(0)+"]";
		mes "Hmm...";
		mes "Vamos ver...";
		mes "De todos os seus itens,";
		mes .@checkitem + " estão danificados.";
		mes "Você gostaria de reparar?";
		next;
		set .@totalcost,.@repairprice*.@checkitem;
		mes "["+getarg(0)+"]";
		if (checkversion(0) == -1)
			mes "Cada reparo custa 1 Aço. Então para reparar todos os itens vou precisar de " + .@checkitem + " Aços! Você gostaria de reparar todos os itens?";
		else
			mes "Cada reparo custa " + .@repairprice + " Zenys. Então para reparar todos os itens vou precisar de " + .@totalcost + " Zenys! Você gostaria de reparar todos os itens?";
		next;
		if (select("Sim:Não") == 1) {
			if (checkversion(0) == -1 && countitem(999) < .@checkitem) {
				mes "["+getarg(0)+"]";
				mes "Ei ei...";
				mes "Olhe o seu inventário antes de pedir para repararem um item, cara! Eu não posso reparar nada porque você não possui material suficiente.";
				close;
			}
			else if (Zeny < .@totalcost) {
				mes "["+getarg(0)+"]";
				mes "Ei ei...";
				mes "Olhe a sua carteira antes de pedir para repararem um item, cara! Eu não posso reparar nada porque você não possui Zeny suficiente.";
				close;
			}
			set .@checkitem2,1;
			while (1) {
				if (getbrokenid(.@checkitem2) == 0) {
					break;
				}
				set .@checkitem2,.@checkitem2+1;
			}
			set .@checkitem2,.@checkitem2-1;
			if (.@checkitem == .@checkitem2) {
				if (checkversion(0) == -1)
					delitem 999,.@checkitem;
				else
					set Zeny, Zeny-.@totalcost;
				while (.@checkitem) {
					repair(.@checkitem);
					set .@checkitem,.@checkitem-1;
				}
				mes "["+getarg(0)+"]";
				mes "Okay! Tudo pronto. Agora, tente ter um pouco mais de cuidado. Você sabe, itens tem vida também.";
				close;
			}
			else {
				mes "["+getarg(0)+"]";
				mes "Mmm? Tem algo errado. Espere... Pegue os itens que você quer reparar e volte aqui.";
				close;
			}
		} else {
			mes "["+getarg(0)+"]";
			mes "Bem, não é da minha conta, mas não é bom levar itens quebrados embora. Você deve repará-los o mais rápido possivel!";
			close;
		}
	} else {
		mes "["+getarg(0)+"]";
		mes "Hohoho...";
		mes "Você não tem nada para fazer aqui se você não tiver itens para reparar.";
		close;
	}
}

// +11 and above Refiners
//============================================================
prt_in,90,72,5	script#re	Bestry#prt	4_M_DWARF,{
	callfunc "refinenew","Bestry",0;
	end;
}
morocc_in,64,41,5	script#re	Bestry#moc	4_M_DWARF,{
	callfunc "refinenew","Bestry",0;
	end;
}
payon_in01,18,132,3	script#re	Bestry#pay	4_M_DWARF,{
	callfunc "refinenew","Bestry",0;
	end;
}

//============================================================
// +11 and above Refiner Function
//============================================================
//= To allow auto safe refining/multiple refining set the
//= second argument to '1' in the function call.
//= If you enable this function, be sure to edit the value of
//= .@safe to the max safe refine in refine_db.txt as well.
//=
//= On official servers, if an item is unsuccessfully refined
//= it will break at a 20% rate and downgrade at an 80% rate.
//============================================================
function	script	refinenew	{
	disable_items;
	mes "["+ getarg(0) +"]";
	mes "I am the best Blacksmith ever!";
	mes "I don't work with normal, boring items.";
	mes "But only with items that are level 10 or higher!";
	next;
	mes "["+ getarg(0) +"]";
	mes "Anyway, you may use my services if your item is lv 10 or higher.";
	mes "What do you want to have refined?";
	next;

	setarray .@position$[1],"Head","Body","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2","Head 2","Head 3";
	set .@menu$,"";
	for(set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@i)) {
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";
			set .@equipped,1;
		}
		set .@menu$, .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "[" + getarg(0) + "]";
		mes "I don't think I can refine any items you have...";
		close;
	}
	set .@part, select(.@menu$);
	if(!getequipisequiped(.@part)) { //custom check
		mes "[" + getarg(0) + "]";
		mes "You're not wearing";
		mes "anything there that";
		mes "I can refine.";
		emotion e_an;
		close;
	}
	//Check if the item is refinable...
	if (!getequipisenableref(.@part)) {
		mes "[" + getarg(0) + "]";
		mes "I don't think I can";
		mes "refine this item at all...";
		close;
	}
	//Check to see if the items is at least +10
	if (getequiprefinerycnt(.@part) < 10) {
		mes "["+ getarg(0) +"]";
		mes "I said I don't work with Equipment under lv. 10.";
		close;
	}
	if (getequiprefinerycnt(.@part) >= 20) {
		mes "["+ getarg(0) +"]";
		mes "I can't refine this";
		mes "any more. This is as";
		mes "refined as it gets!";
		close;
	}
	set .@refineitemid, getequipid(.@part); // save id of the item
	set .@refinerycnt, getequiprefinerycnt(.@part); //save refinery count
	if ((getequipweaponlv(.@part) >= 1) && (getequipweaponlv(.@part) <= 4)) {
		set .@material,6224;
		set .@price,100000;
		switch(getequipweaponlv(.@part)) {
			case 1: set .@safe,10; break;
			case 2: set .@safe,10; break;
			case 3: set .@safe,10; break;
			case 4: set .@safe,10; break;
		}
		mes "["+ getarg(0) +"]";
		mes "Hmm a weapon, is that ok?";
		mes "If you want to refine this weapon,";
		mes "I will need 1 ^003366Bradium^000000 and 100,000 zeny.";
		mes "Are you sure you want to continue?";
	} else {
		set .@material,6223;
		set .@price,100000;
		set .@safe,10;
		mes "["+ getarg(0) +"]";
		mes "Hmm an armor, is that ok?";
		mes "If you want to refine this armor,";
		mes "I will need 1 ^003366Carnium^000000 and 100,000 zeny.";
		mes "Are you sure you want to continue?";
	}
	next;
	if (select("Yes:No") == 2){
		mes "["+ getarg(0) +"]";
		mes "Well, no challenge is one way...";
		mes "No risk... that could be wise.";
		close;
	}
	if (getarg(1) != 1) {
		if ((getequipweaponlv(.@part) >= 1) && (getequipweaponlv(.@part) <= 4)) {
			mes "["+ getarg(0) +"]";
			mes "This weapon already has been refined serveral times.";
			mes "It could be destroyed if you try more.";
			mes "It won't break 100%, but is has a small chance.";
			next;
			mes "["+ getarg(0) +"]";
			mes "You could have the upgrade level of the weapon decreased,";
			mes "or if it breaks, you will lose ^FF0000any cards^000000 or special properties added to it.";
		} else {
			mes "["+ getarg(0) +"]";
			mes "This armor already has been refined serveral times.";
			mes "It could be destroyed if you try more.";
			mes "It won't break 100%, but is has a small chance.";
			next;
			mes "["+ getarg(0) +"]";
			mes "You could have the upgrade level of the armor decreased,";
			mes "or if it breaks, you will lose ^FF0000any cards^000000 or special properties added to it.";
		}
		next;
		mes "["+ getarg(0) +"]";
		mes "Do you want me to refine it?";
		mes "I think I gave you enough warnings.";
		next;
		if (select("Yes.:No.") == 2) {
			mes "["+ getarg(0) +"]";
			mes "Well, no challenge is one way..";
			mes "No risk.. that could be wise.";
			close;
		}
		if (countitem(.@material) < 1 || Zeny < .@price) {
			mes "["+ getarg(0) +"]";
			mes "Hm, it seems you don't have enough materials or money.";
			mes "Please check it out.";
			close;
		}
		set Zeny,Zeny - .@price;
		delitem .@material,1;
		if (getequipisequiped(.@part) == 0) { // hacker has removed the item (not changed, why?)
			mes "[" + getarg(0) + "]";
			mes "Look here... you don't have any items on...";
			close;
		}
		if (getequiprefinerycnt(.@part) != .@refinerycnt || getequipid(.@part) != .@refineitemid) { // hacker has changed the item
			mes "[" + getarg(0) + "]";
			emotion e_an;
			mes "Wait a second...";
			mes "Do you think I'm stupid?!";
			mes "You switched the item while I wasn't looking! Get out of here!";
			close;
		}
		if (getequippercentrefinery(.@part) > rand(100)) {
			mes "Clang! Clang! Clang! Clang!";
			successrefitem .@part;
			next;
			emotion e_no1;
			mes "["+ getarg(0) +"]";
			mes "Good! Succes!!!";
			mes "I am the best Blacksmith.";
			close;
		} else {
			if (rand(100) < 80) {
				mes "["+ getarg(0) +"]";
				mes "Clang! Clang! Clang! Clang!";
				downrefitem .@part,3;
				next;
				emotion (!rand(5))?e_cash:e_omg;
				mes "["+ getarg(0) +"]";
				mes "Ahhh!!!";
				next;
				mes "["+ getarg(0) +"]";
				mes "Oh my!";
				mes "The upgrade level has dropped...";
				mes "There could've been made an mistake even though I am the best ever.";
				mes "It was out of my hands.";
				next;
				mes "["+ getarg(0) +"]";
				mes "I will do a better job next time! Don't worry!";
				close;
			} else {
				mes "["+ getarg(0) +"]";
				mes "Clang! Clang! Clang!";
				failedrefitem .@part;
				next;
				emotion (!rand(5))?e_cash:e_omg;
				mes "["+ getarg(0) +"]";
				mes "Hmmm!";
				next;
				mes "["+ getarg(0) +"]";
				mes "Oh my! I've failed to refine stuff...";
				mes "I didn't mean it!";
				mes "There could've been made an mistake even though I am the best ever.";
				mes "It was out of my hands.";
				next;
				mes "["+ getarg(0) +"]";
				mes "I will do a better job next time! Don't worry!";
				close;
			}
		}
	}
// New +11 and above Refining Functions ========================
	if (getequiprefinerycnt(.@part) < .@safe) {
		mes "[" + getarg(0) + "]";
		mes "I can refine this to the safe limit or a desired number of times. It's your choice.";
		next;
		set .@menu2,select("To the safe limit, please.","I'll decide how many times.","I've changed my mind...");
	} else
		set .@menu2,2;
	switch(.@menu2){
	case 1: 
		set .@refinecnt,.@safe - getequiprefinerycnt(.@part);
		break;
	case 2:
		mes "[" + getarg(0) + "]";
		mes "How many times would you like me to refine your item?";
		next;
		input .@refinecnt;
		set .@refinecheck,.@refinecnt + getequiprefinerycnt(.@part);
		if (.@refinecnt < 1 || .@refinecheck > 20) {
			mes "[" + getarg(0) + "]";
			mes "I can't refine this item that many times.";
			close;
		}
		if (.@refinecheck > .@safe) {
			set .@refinecheck,.@refinecheck - .@safe;
			mes "[" + getarg(0) + "]";
			mes "This will try to refine the equipment " + .@refinecheck + " times past the safe limit. Your equipment may be destroyed... is that ok?";
			next;
			if (select("Yes...","No...") == 2){
				mes "[" + getarg(0) + "]";
				mes "You said so... So be it.";
				close;
			}
		}
		break;
	case 3:
		mes "[" + getarg(0) + "]";
		mes "You said so... So be it.";
		close;
	}
	set .@fullprice,.@price * .@refinecnt;
	mes "[" + getarg(0) + "]";
	mes "That will cost you " + .@refinecnt + " " + getitemname(.@material) + " and " + .@fullprice + " Zeny. Is that ok?";
	next;
	if (select("Yes","No...") == 2){
		mes "[" + getarg(0) + "]";
		mes "You said so... So be it.";
		close;
	}
	if (countitem(.@material) < .@refinecnt || Zeny < .@fullprice) {
		mes "[" + getarg(0) + "]";
		mes "Is that all you got? Unfortunately I can't work for you at a lower price. Try putting yourself in my shoes.";
		close;
	}
	set Zeny,Zeny - .@fullprice;
	delitem .@material,.@refinecnt;
	while(.@refinecnt){
		if (getequipisequiped(.@part) == 0) {
			mes "[" + getarg(0) + "]";
			mes "Look here... you don't have any items on...";
			close;
		}
		if (getequipid(.@part) != .@refineitemid || (.@menu2 == 1 && getequippercentrefinery(.@part) < 100)) {
			mes "[" + getarg(0) + "]";
			mes "Clang... No, but did you imagine I could be so stupid?!";
			mes "You changed it...";
			mes "Get out before I stun you with my Hammer!!";
			close;
		}
		if (getequippercentrefinery(.@part) > rand(100)) {
			mes "Clang! Clang! Clang! Clang!";
			successrefitem .@part;
			set .@refinecnt,.@refinecnt - 1;
			next;
		} else {
			if (rand(100) < 80) {
				mes "["+ getarg(0) +"]";
				mes "Clang! Clang! Clang! Clang!";
				downrefitem .@part,3;
				next;
				emotion (!rand(5))?e_cash:e_omg;
				mes "["+ getarg(0) +"]";
				mes "Ahhh!!!";
				next;
				mes "["+ getarg(0) +"]";
				mes "Oh my!";
				mes "The upgrade level has dropped...";
				mes "There could've been made an mistake even though I am the best ever.";
				mes "It was out of my hands.";
				next;
				mes "["+ getarg(0) +"]";
				mes "I will do a better job next time! Don't worry!";
				close;
			} else {
				mes "["+ getarg(0) +"]";
				mes "Clang! Clang! Clang!";
				failedrefitem .@part;
				next;
				emotion (!rand(5))?e_cash:e_omg;
				mes "["+ getarg(0) +"]";
				mes "Hmmm!";
				next;
				mes "["+ getarg(0) +"]";
				mes "Oh my! I've failed to refine stuff...";
				mes "I didn't mean it!";
				mes "There could've been made an mistake even though I am the best ever.";
				mes "It was out of my hands.";
				next;
				mes "["+ getarg(0) +"]";
				mes "I will do a better job next time! Don't worry!";
				close;
			}
		}
	}
	emotion e_no1;
	mes "["+ getarg(0) +"]";
	mes "Good! Succes!!!";
	mes "I am the best Blacksmith.";
	close;
}

// Ori/Elu to Carnium/Bradium Refiners
//============================================================
prt_in,85,71,5	script#re	Austry#ref	4_M_DWARF,{
	mes "[Austry]";
	mes "If you bring me 3";
	mes "Oridecon or Elunium";
	mes "I can exchange them for";
	mes "Bradium or Carnium.";
	mes "Just give me 50,000z.";
	next;
	switch(select("Oridecon to Bradium.:Elunium to Carnium.:Purified Bradium to Carnium.:No thanks.")) {
	case 1:
		setarray .@i[0],984,3,6224;  //Oridecon -> Bradium
		break;
	case 2:
		setarray .@i[0],985,3,6223;  //Elunium -> Carnium
		break;
	case 3:
		setarray .@i[0],6090,1,6223; //Purified_Bradium -> Carnium
		break;
	case 4:
		mes "[Austry]";
		mes "Hmm...";
		close;
	}
	if (countitem(.@i[0]) >= .@i[1] && Zeny >= 50000) {
		delitem .@i[0],.@i[1];
		set Zeny, Zeny - 50000;
		getitem .@i[2],1;
		mes "[Austry]";
		if (.@i[0] == 6090) {
			mes "Refining with Purified Bradium";
			mes "is a little expensive. I can";
			mes "trade it for some Carnium.";
		} else
			mes "Ok! Here is your "+getitemname(.@i[2])+".";
		mes "Take it and use it well.";
		close;
	}
	mes "[Austry]";
	mes "You better not be trying";
	mes "to cheat me because you";
	mes "don't have enough zeny";
	mes "or "+getitemname(.@i[0])+".";
	close;
}
payon_in01,14,125,5	duplicatr(Austry#ref)	Austry#pay	4_M_DWARF,{}
payon,143,165,4	duplicata(Repairman#alb)	Repairman#pay	4_M_ORIENT01

// Paul Spanner: Einbroch Blacksmith Supplier
//============================================================
ein_in01,38,29,0	script#dv	Paul Spanner	1_M_SMITH,{
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	mes "[Paul Spanner]";
	mes "Welcome, my friend.";
	mes "In my shop, you will find everything that you need in forging.";
	mes "Tell me what you need.";
	next;
	switch(select("Purchase Anvil.:Purchase Forging Items.:Purchase Metal.:Process Ores.:Quit.")) {
	case 1:
		mes "[Paul Spanner]";
		mes "Anvil is the most necessary item for Blacksmiths.";
		mes "Since you will use an Anvil more than once, you'd better buy a nice one.";
		next;
		switch(select("Anvil - 30,000z.:Oridecon Anvil - 120,000z.:Golden Anvil - 300,000z.:I need a better anvil.:Cancel.")) {
		case 1:
			if (Zeny < 30000) {
				mes "[Paul Spanner]";
				mes "With that much of money, you cannot even buy a toy anvil!";
				close;
			}
			getitem 986,1; //Anvil
			set Zeny, Zeny-30000;
			mes "[Paul Spanner]";
			mes "It is the cheapest anvil which has the most basic ability.";
			mes "Thank you for using my shop. If you need anything, just let me know.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[Paul Spanner]";
				mes "With that much of money, you cannot even buy a toy anvil!";
				close;
			}
			getitem 987,1; //Oridecon_Anvil
			set Zeny, Zeny-120000;
			mes "[Paul Spanner]";
			mes "Ah, you have an eye for anvil. A Blacksmith needs an anvil at least as good as this.";
			mes "Thank you for using my shop. If you need anything, just let me know.";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[Paul Spanner]";
				mes "With that much of money, you cannot even buy a toy anvil!";
				close;
			}
			getitem 988,1; //Golden_Anvil
			set Zeny, Zeny-300000;
			mes "[Paul Spanner]";
			mes "I can tell your ambition to become a good Blacksmith just by looking at you to choose this Golden Anvil!";
			mes "This anvil will surely aid you in creating the best weapons.";
			close;
		case 4:
			mes "[Paul Spanner]";
			mes "I am sorry, but I do not sell better anvils than Golden Anvil.";
			mes "Unless you find the legendary anvil of 'Linggell', I don't think that you could find better one than Golden Anvil in any other places.";
			close;
		case 5:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
	case 2:
		mes "[Paul Spanner]";
		mes "You need various materials to process ores and to forge weapons.";
		mes "I have everything that you need. Take a look.";
		next;
		switch(select("Mini Furnace - 150z.:Iron Hammer - 1,000z.:Golden Hammer - 3,000z.:Oridecon Hammer - 5,000z.:Cancel.")) {
		case 1:
			set .@item,612;
			set .@item_cost,150;
			set .@item_weight,200;
			mes "[Paul Spanner]";
			mes "You definately need this furnce to process ores!";
			next;
			break;
		case 2:
			set .@item,613;
			set .@item_cost,1000;
			set .@item_weight,200;
			break;
		case 3:
			set .@item,614;
			set .@item_cost,3000;
			set .@item_weight,300;
			break;
		case 4:
			set .@item,615;
			set .@item_cost,5000;
			set .@item_weight,400;
			break;
		case 5:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
		mes "[Paul Spanner]";
		mes "So, how many do you need? If you want to cancel the trade, enter '0'.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "You have canceled the trade. If you need anything, just let me know.";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "You can only buy 500 or less at a time.";
				next;
			}
			else {
				break;
			}
		}
		set .@sell,.@input * .@item_cost;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "You don't have enough money. Sorry, I cannot sell them at a loss.";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "Hey, you look pale. Why don't you go lighten your weight first.";
			close;
		}
		set Zeny, Zeny-.@sell;
		getitem .@item,.@input;
		mes "[Paul Spanner]";
		mes "Thank you for using my shop. If you need anything, just let me know.";
		close;
	case 3:
		mes "[Paul Spanner]";
		mes "I have high quality metal.";
		mes "So, which metal would you like to buy?";
		next;
		switch(select("Phracon - 200z.:Emveretarcon - 1,000z.:Quit.")) {
		case 1:
			set .@item,1010;
			set .@item_price,200;
			break;
		case 2:
			set .@item,1011;
			set .@item_price,1000;
			break;
		case 3:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
		mes "[Paul Spanner]";
		mes "So, how many of them do you need? If you want to cancel the trade, enter '0'.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "The trade has been canceled. If you need anything, just let me know.";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "You can buy 500 or less at a time.";
				next;
			}
			else {
				break;
			}
		}
		set .@sell,.@input * .@item_price;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "You don't have enough money. Sorry, I cannot sell them at a loss.";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "Hey, you look pale. Why don't you go lighten your weight first?";
			close;
		}
		getitem .@item,.@input;
		set Zeny, Zeny-.@sell;
		mes "[Paul Spanner]";
		mes "Thank you for using my shop. If you need anything, just let me know.";
		close;
	case 4:
		mes "[Paul Spanner]";
		mes "I can process Oridecon and Elunium for you.";
		mes "You need 5 ores to process them into one Oridecon or Elunium.";
		mes "So, which one do you want to process?";
		switch(select("Oridecon:Elunium:Quit.")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[Paul Spanner]";
				mes "You need 5 ores to process them into one pure Oridecon.";
				close;
			}
			else {
				delitem 756,5; //Oridecon_Stone
				getitem 984,1; //Oridecon
				mes "[Paul Spanner]";
				mes "There you go. Thank you for using my service.";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[Paul Spanner]";
				mes "You need 5 ores to process them into one pure Elunium.";
				close;
			}
			else {
				delitem 757,5; //Elunium_Stone
				getitem 985,1; //Elunium
				mes "[Paul Spanner]";
				mes "There you go. Thank you for using my service.";
				close;
			}
		case 3:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
	case 5:
		mes "[Paul Spanner]";
		mes "If you need anything, just let me know.";
		close;
	}
}

morocc_in,60,38,5	duplicatr(Austry#ref)	Austry#moc	4_M_DWARF

// Malangdo Refiner
//============================================================
malangdo,224,172,6	script#re	Clink#mal_normal	4_CAT_SAILOR1,{
	disable_items;
	mes "[Clink]";
	mes "My cool dad Holink said I have the world's greatest refine hammer!!";
	mes "Meow Meow~";
	mes "Who do you think I am?";
	mes "Yes!!! You!! You want to refine?";
	next;
	setarray .@position$[1],"Head","Body","Left Hand","Right Hand","Robe","Shoes","Accessory 1","Accessory 2","Head 2","Head 3";
	set .@menu$,"";
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$+((getequipisequiped(.@i))?getequipname(.@i):.@position$[.@i]+"-[Empty]")+":";
	set .@part, select(.@menu$);
	if (!getequipisequiped(.@part)) {
		mes "[Clink]";
		switch(.@part) {
		case 1:
			mes "Dad said. There's no cure for stupidity...";
			break;
		case 2:
			mes "There's nothing to see here!!";
			break;
		case 3:
			mes "What an arrogant left hand this is!";
			break;
		case 4:
			mes "What an arrogant right hand this is!";
			break;
		case 5:
			mes "Get that dirty thing off my face!!";
			break;
		case 6:
			mes "Kyaong~! Do not provoke me.";
			break;
		case 7:
		case 8:
			mes "Where is the accessory?";
			break;
		case 9:
		case 10:
			mes "Are you talking about the other head part?";
			break;
		}
		close;
	}
	if (!getequipisenableref(.@part)) {
		mes "[Clink]";
		mes "This can't be refined!!";
		close;
	}
	if (getequiprefinerycnt(.@part) >= 10) {
		mes "[Clink]";
		mes "Perfect refining. Did I do this for you?";
		close;
	}
	mes "[Clink]";
	switch(getequipweaponlv(.@part)) {
	default:
	case 0: // Armor
		set .@price,2000;
		set .@material,985; //Elunium
		set .@type$,"armor";
		mes "Hmm, an armor refine? Someone like you?";
		break;
	case 1: // Level 1 Weapon
		set .@price,50;
		set .@material,1010; //Phracon
		set .@type$,"weapon";
		mes "A level 1 weapon?";
		mes "Urr... Annoying... Okay, let's try...";
		break;
	case 2: // Level 2 Weapon
		set .@price,200;
		set .@material,1011; //Emveretarcon
		set .@type$,"weapon";
		mes "A level 2 weapon?";
		break;
	case 3: // Level 3 Weapon
		set .@price,20000;
		set .@material,984; //Oridecon
		set .@type$,"weapon";
		mes "Woot!! A level 3 weapon? Impressive~";
		break;
	case 4: // Level 4 Weapon
		set .@price,50000;
		set .@material,984; //Oridecon
		set .@type$,"weapon";
		mes "Wow!... A level 4 weapon~!!";
		break;
	}
	mes "You need ^ff9999"+getitemname(.@material)+"^000000 and ^ff9999"+.@price+"^000000 Zeny. Do you have them?";
	next;
	if (select("Yes, I do!!:Forget about it!!") == 2) {
		mes "[Clink]";
		mes "I knew it!!";
		mes "I knew you were not worth trying my magical refining hammer for.";
		close;
	}
	if (getequippercentrefinery(.@part) < 100) {
		mes "[Clink]";
		mes "Wow!!";
		mes "This "+.@type$+" has been refined quite a bit, huh?";
		mes "You do know that this might break, right?";
		next;
		mes "[Clink]";
		mes "If you break the "+.@type$+", you can never use it again.";
		mes "Cards and enchant effects...";
		mes "the ^ff0000whole thing will disappear^000000.";
		mes "You still up for this~?";
		next;
		if (select("Yes, I am!!:Forget about it!!") == 2) {
			mes "[Clink]";
			mes "I knew it!!";
			mes "You can't even take this big step. Don't think about refining...";
			close;
		}
	}
	if (countitem(.@material) == 0 || Zeny < .@price) {
		mes "[Clink]";
		mes "Hey you!! Didn't I tell you";
		mes "that you need ^ff9999"+getitemname(.@material)+"^000000 and ^ff9999"+.@price+"^000000 Zeny??!!";
		close;
	}
	delitem .@material,1;
	set Zeny, Zeny-.@price;
	if (getequippercentrefinery(.@part) <= rand(100)) {
		failedrefitem .@part;
		mes "[Clink]";
		mes "Cry Hammer!! Cry!!!";
		next;
		switch(rand(1,5)) {
			case 1: emotion e_sob; break;
			case 2: emotion e_swt2; break;
			case 3: emotion e_wah; break;
			case 4: emotion e_hmm; break;
			case 5: emotion e_lv2; break;
		}
		mes "[Clink]";
		mes "Huh?! I failed?!";
		next;
		mes "[Clink]";
		mes "Arrgg~ It's all~ Broken...? What a pity~";
		next;
		mes "[Clink]";
		mes "Hey...!! Get me another one.";
		mes "This is not possible.";
		mes "How can my hammer fail from refining?";
		close;
	}
	successrefitem .@part;
	mes "[Clink]";
	mes "Cry Hammer!! Cry!!!";
	next;
	emotion e_kis;
	mes "[Clink]";
	mes "Ok!! Perfect!!";
	mes "There's nothing I can't refine";
	mes "with this special hammer.";
	mes "You can praise me!!";
	mes "What a day!!";
	close;
}
