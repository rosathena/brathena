//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: rachel.txt                             |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.0/1.1/1.4                         |
//   |--------------------------------------------------------|
//   | Descrição: Guias da Cidade de Rachel.                  |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Otimização [Holy & Hold]                           |
//   | 1.2 Tradução PT-BR [Holy & Protimus]                   |
//   | 1.3 Formatação nos Diálogos [lututuiBR & Holy]         |
//   | 1.4 Atualização no sistema de navegação [lututuiBR]    |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

rachel,138,146,5	script	Guia de Rachel#rachel	4_M_RASWORD,{
	mes "[Guia de Rachel]";
	if (checkversion(0)) {
		mes "Bem-vindo a Capital de Arunafeltz, ^8B4513Rachel^000000 onde o sopro quente da deusa Freya alcança.";
		mes "Se essa é sua primeira que utiliza meus serviços, sugiro que escolha uma das opções:";
		F_Navi("[Guia de Rachel]");
		next;
		while (1) {
			switch(select("[ Principais Lugares ]:[ Mercadores & Ajudantes ]:Remover marcadores do mini-mapa:Cancelar")) {
			case 1:
				set .@loop,1;
				while (.@loop) {
					switch(select("[ Hotel ]:[ Loja de Armas ]:[ Loja de Utilidades ]:[ Aeroporto ]:[ Cheshrumnir ]:[ Mansão do Sacerdote Zed ]:Menu Anterior")) {
					case 1:
						mes "[Guia de Rachel]";
						mes "Você pode repousar de sua viagem no "+F_Navi("Hotel","rachel,115,144")+". O edifício a minha esquerda é conhecido como o Hotel de Rachel.";
						mes "Gostaria de verificar outras localizações?";
						viewpoint 1,115,144,0,0xFF0000;
						next;
						break;
					case 2:
						mes "[Guia de Rachel]";
						mes "Você quer verificar as Armas que são vendidas em Rachel? A "+F_Navi("Loja de Armas","rachel,42,82")+" está localizada perto do portão Oeste.";
						mes "Gostaria de verificar outras localizações?";
						viewpoint 1,42,82,1,0xFF4500;
						next;
						break;
					case 3:
						mes "[Guia de Rachel]";
						mes "A "+F_Navi("Loja de Utilidades","rachel,83,73")+" de Rachel vende as melhores poções. Ela está localizada próxima ao portão oeste. Gostaria de verificar outras localizações?";
						viewpoint 1,83,73,2,0xDAA520;
						next;
						break;
					case 4:
						mes "[Guia de Rachel]";
						mes "O "+F_Navi("Aeroporto","rachel,272,125")+" está localizado fora do portão leste. Gostaria de verificar outras localizações?";
						viewpoint 1,272,125,3,0x008000;
						next;
						break;
					case 5:
						mes "[Guia de Rachel]";
						mes F_Navi("Cheshrumnir","rachel,150,243")+" é um solo sagrado onde papisa, que é a encarnação da Deusa Freya, permanece. Pegue a estrada ao norte para encontrar o edifício. Gostaria de verificar outras localizações?";
						viewpoint 1,150,243,4,0x0000FF;
						next;
						break;
					case 6:
						mes "[Guia de Rachel]";
						mes "Deixe-me marcar para você a localização da "+F_Navi("Mansão do Sacerdote Zed","rachel,98,238","^000080")+" em seu mini-mapa. Gostaria de verificar outras localizações?";
						viewpoint 1,98,238,5,0x000080;
						next;
						break;
					case 7:
						set .@loop,0;
						break;
					}
				}
				break;
			case 2:
				set .@loop,1;
				while (.@loop) {
					switch(select("[ Corporação de Eventos Incrívels ]:[ Oficial do Éden ]:Menu Anterior")) {
					case 1:
						mes "[Guia de Rachel]";
						mes "Deixe-me marcar para você a localização da "+F_Navi("Corporação de Eventos Incrívels","rachel,110,138","^803232")+" em seu mini-mapa.";
						mes "Gostaria de verificar outras localizações?";
						viewpoint 1,110,138,7,0x803232;
						next;
						break;
					case 2:
						mes "[Guia de Rachel]";
						mes "Deixe-me marcar para você a localização do "+F_Navi("Oficial do Éden","rachel,125,144","^556B2F")+" em seu mini-mapa.";
						mes "Gostaria de verificar outras localizações?";
						viewpoint 1,125,144,8,0x556B2F;
						next;
						break;
					case 3:
						set .@loop,0;
						break;
					}
				}
				break;
			case 3:
				mes "[Guia de Rachel]";
				mes "Claro, irei remover todos os pontos marcados de seu mini-mapa.";
				mes "Há mais algo que eu possa fazer por você?";
				viewpoint 2,115,144,0,0xFFFFFF;
				viewpoint 2,42,82,1,0xFFFFFF;
				viewpoint 2,83,73,2,0xFFFFFF;
				viewpoint 2,272,125,3,0xFFFFFF;
				viewpoint 2,150,243,4,0xFFFFFF;
				viewpoint 2,98,238,5,0xFFFFFF;
				viewpoint 2,110,138,7,0xFFFFFF;
				viewpoint 2,125,144,8,0xFFFFFF;
				next;
				break;
			case 4:
				mes "[Guia de Rachel]";
				mes "Aproveite sua estadia!";
				close;
			}
		}
	} else {
		mes "Bem-vindo à capital de Arunafeltz, Rachel, onde contamos com a proteção de nossa deusa Freya.";
		mes "Se essa é a primeira vez que você usa nossos serviços de guia, por que não consulta o menu “Aviso” primeiro?";
		set .@loop1,1;
		while(.@loop1) {
			next;
			switch(select("Indicações.:Limpar as indicações do minimapa.:Aviso.:Cancelar")) {
			case 1:
				mes "[Guia de Rachel]";
				mes "Eu posso dizer a você onde fica cada edifício que procura, desde que ele fique em Rachel.";
				mes "E então, onde você gostaria de ir?";
				if (.@compass_check == 0) {
					mes "Você gostaria que eu indicasse os locais em seu Minimapa?";
					next;
					if (select("Sim.:Não") == 1) set .@compass_check,1;
				}
				set .@loop2,1;
				while(.@loop2) {
					if (.@wait_button_chk == 0) set .@wait_button_chk,1;
					else next;
		
					switch(select("Cheshrumnir.:Pousada.:Loja de Armas.:Loja de Utilidades.:Aeroporto.:Cancelar")) {
					case 1:
						mes "[Guia de Rachel]";
						mes "Cheshrumnir é uma área sagrada onde se encontra a papisa, a encarnação da deusa Freya.";
						mes "Pegue a rua ao norte para encontrar o edifício.";
						if (.@compass_check == 1) viewpoint 1,150,249,1,0xFF0000;
						break;
					case 2:
						mes "[Guia de Rachel]";
						mes "Você pode descansar na Pousada.";
						mes "O edifício a minha esquerda é a Pousada de Rachel.";
						if (.@compass_check == 1) viewpoint 1,115,149,2,0xFF00FF;
						break;
					case 3:
						mes "[Guia de Rachel]";
						mes "Você gostaria de verificar as armas que são vendidas em Rachel?";
						mes "A Loja de Armas está localizada próxima ao portão oeste.";
						if (.@compass_check == 1) viewpoint 1,42,87,3,0x99FFFF;
						break;
					case 4:
						mes "[Guia de Rachel]";
						mes "A Loja de Utilidades de Rachel vende poções da melhor qualidade.";
						mes "Ela está localizada perto do portão oeste.";
						if (.@compass_check == 1) viewpoint 1,83,78,4,0x0000FF;
						break;
					case 5:
						mes "[Guia de Rachel]";
						mes "O Aeroporto está localizado do lado de fora do portão leste.";
						if (.@compass_check == 1) viewpoint 1,273,125,5,0x00FF00;
						break;
					case 6:
						mes "[Guia de Rachel]";
						mes "Se você quiser limpar as indicações no seu MiniMapa, fale comigo novamente e escolha a opção 'Limpar as indicações do Minimapa'.";
						set .@loop2,0;
						break;
					}
				}
				break;
		
			case 2:
				viewpoint 2,150,249,1,0xFF0000;
				viewpoint 2,115,149,2,0xFF00FF;
				viewpoint 2,42,87,3,0x99FFFF;
				viewpoint 2,83,78,4,0x0000FF;
				viewpoint 2,273,125,5,0x00FF00;
				mes "[Guia de Rachel]";
				mes "Pronto, elas foram eliminadas.";
				mes "Se você quiser mais indicações é só pedir.";
				break;
			case 3:
				mes "[Guia de Rachel]";
				mes "Quando você usar o menu 'Guia da Vila' certifique-se que as localizações dos edifícios serão marcadas no seu mini-mapa no canto direito e acima de sua tela.";
				mes "Se você não puder ver seu mini-mapa utilize o comando 'ctrl+tab' ou o botão 'Mapa' da janela de informações básicas.";
				mes "E você também pode ajustar o zoom de seu mini-mapa utilizando o botão '-' caso você não consiga ver o mapa inteiro da Vila.";
				break;
			case 4:
				mes "[Guia de Rachel]";
				mes "Desejo uma ótima estadia em Arunafeltz.";
				close2;
				set .@loop1,0;
				break;
			}
		}
		close;
	}
}