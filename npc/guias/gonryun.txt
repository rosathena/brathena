//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: gonryun.txt                            |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.0/1.2/1.4                         |
//   |--------------------------------------------------------|
//   | Descrição: Guias da Cidade de Kunlun.                  |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Otimização [Holy & Hold]                           |
//   | 1.2 Tradução PT-BR [Protimus, lututuiBR & Holy]        |
//   | 1.3 Formatação nos Diálogos [lututuiBR & Holy]         |
//   | 1.4 Atualização no sistema de navegação [lututuiBR]    |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

gonryun,163,60,4	script	Guia de Kunlun#01gonryun	8_M_TWSOLDIER,{
	mes "[He Yuen Zhe]";
	mes "Bem-vindo a Kunlun, uma cidade cheia de paixão e energia!";
	mes "Você pode sentir a alegria só de passear pela cidade!";
	next;
	mes "[He Yuen Zhe]";
	mes "Eu sou o responsável por responder suas dúvidas sobre a cidade.";
	mes "Por favor, sinta-se à vontade para perguntar.";
	F_Navi("[He Yuen Zhe]");
	if (checkversion(0) == 1) {
		while (1) {
			next;
			switch(select("[ Funcionária Kafra ]:[ Residência do Chefe ]:[ Vendedor de Utilidades ]:[ Vendedor de Armas ]:[ Vendedor de Armaduras ]:[ Fabricante de Vinho ]:[ Embaixador de Kunlun ]:Remover indicações do Mini-Mapa:Cancelar")) {
			case 1:
				callsub L_Mark,"^0000FFFuncionária Kafra^000000";
				viewpoint 1,159,122,0,0x0A82FF;
				break;
			case 2:
				callsub L_Mark, F_Navi("Residência do Chefe","gonryun,110,131","^006400");
				viewpoint 1,110,131,1,0xAAFF00;
				break;
			case 3:
				callsub L_Mark, F_Navi("Vendedor de Utilidades","gonryun,147,84","^D8BFD8");
				viewpoint 1,147,84,2,0xD8BFD8;
				break;
			case 4:
				callsub L_Mark, F_Navi("Vendedor de Armas","gonryun,174,101","^FF1493");
				viewpoint 1,174,101,3,0xFF1493;
				break;
			case 5:
				callsub L_Mark, F_Navi("Vendedor de Armaduras","gonryun,173,84","^8B4513");
				viewpoint 1,173,84,4,0x8B4513;
				break;
			case 6:
				callsub L_Mark, F_Navi("Fabricante de Vinho","gonryun,213,115","^9400D3");
				viewpoint 1,213,115,5,0x9400D3;
				break;
			case 7:
				callsub L_Mark, F_Navi("Embaixador de Kunlun","gonryun,161,11","^00BFFF");
				viewpoint 1,161,11,6,0x00BFFF;
				break;
			case 8:
				mes "[He Yuen Zhe]";
				mes "Removerei todas as indicações do seu mini-mapa.";
				mes "Há mais alguma coisa que eu possa fazer por você?";
				viewpoint 2,1,1,0,0xFFFFFF;
				viewpoint 2,1,1,1,0xFFFFFF;
				viewpoint 2,1,1,2,0xFFFFFF;
				viewpoint 2,1,1,3,0xFFFFFF;
				viewpoint 2,1,1,4,0xFFFFFF;
				viewpoint 2,1,1,5,0xFFFFFF;
				viewpoint 2,1,1,6,0xFFFFFF;
				break;
			case 9:
				mes "[He Yuen Zhe]";
				mes "Bon voyage!";
				close;
			}
		}
		end;
	L_Mark:
		mes "[He Yuen Zhe]";
		mes getarg(0)+" está marcado no seu mini-mapa.";
		mes "Verifique seu mini-mapa.";
		mes "Há mais alguma coisa que eu possa fazer por você?";
		return;
	} else {
		switch(select("Residência do Chefe.:Vendedor de Utilidades.:Vendedor de Armas.:Vendedor de Armaduras.:Loja de Vinhos.")) {
		case 1:
			viewpoint 1,109,131,0,0xFF3355;
			mes "[He Yuen Zhe]";
			mes "Veja seu mini-mapa.";
			mes "Se você for no ^FF3355+^000000 encontrará o governador.";
			mes "Aproveite sua estada na adorável Kunlun!";
			close;
		case 2:
			viewpoint 1,147,82,1,0xCE6300;
			mes "[He Yuen Zhe]";
			mes "Veja seu mini-mapa.";
			mes "Se você for no ^CE6300+^000000 encontrará um Vendedor de Utilidades.";
			mes "Aproveite sua estada na adorável Kunlun!";
			close;
		case 3:
			viewpoint 1,174,104,2,0x55FF33;
			mes "[He Yuen Zhe]";
			mes "Veja seu mini-mapa.";
			mes "Se você for no ^55FF33+^000000 encontrará um Vendedor de Armas.";
			mes "Aproveite sua estada na adorável Kunlun!";
			close;
		case 4:
			viewpoint 1,173,84,3,0x3355FF;
			mes "[He Yuen Zhe]";
			mes "Veja seu mini-mapa.";
			mes "Se você for no ^3355FF+^000000 encontrará um Vendedor de Armaduras.";
			mes "Aproveite sua estada na adorável Kunlun!";
			close;
		case 5:
			viewpoint 1,215,114,3,0xCD69C9;
			mes "[He Yuen Zhe]";
			mes "Veja seu mini-mapa.";
			mes "Se você for no ^FFFFFF+^000000 encontrará uma loja de vinhos.";
			mes "Aproveite sua estada na adorável Kunlun!";
			close;
		}
	}
}