//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Quest do Noviço                        |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 2.2/1.1/1.3                         |
//   |--------------------------------------------------------|
//   | Descrição: Mudança de Classe para Noviço.              |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [Holy e Protimus]                   |
//   | 1.2 Otimização [Holy & Hold]                           |
//   | 1.3 Formatação nos Diálogos [lututuiBR & Holy]         |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

prt_church,184,41,4	script	Bispo Tomas Cerbantes#nov	1_M_PASTOR,{
	mes "[Tomas Cerbantes]";
	if (Upper == 1) {
		if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
			if (Class == Job_Novice_High) {
				mes "Ah, eu sinto seu vigor e experiência.";
				mes "Você deve ter aprendido muitas coisas antes de entrar em Valhalla.";
				next;
				mes "[Tomas Cerbantes]";
				if (getskilllv("NV_BASIC") < 9) {
					mes "Infelizmente, eu acho que você ainda não está pronto para se tornar um Noviço.";
					mes "Por favor, conclua o aprendizado de todas as Competências Básicas primeiro.";
					next;
					mes "[Tomas Cerbantes]";
					mes "Entretanto, vou esperar até que você esteja pronto.";
					mes "Que Deus esteja com você.";
					close;
				}
				mes "Bem, eu recebê-o ao voltar de Valhalla e desejo-lhe sorte na sua nova jornada.";
				next;
				skill 143,0,0;
				jobchange Job_Acolyte_High;
				skill 156,1,0;
				mes "[Tomas Cerbantes]";
				mes "Agora, aventure-se e procure aqueles que necessitam de sua ajuda.";
				mes "Que Deus ilumine seu caminho.";
				close;
			}
			mes "[Tomas Cerbantes]";
			mes "Agora, aventure-se e procure aqueles que necessitam de sua ajuda.";
			mes "Que Deus ilumine vocês.";
			close;
		}
		mes "[Tomas Cerbantes]";
		mes "Sinto que você passou por uma experiência de vida passada.";
		mes "Você deve ter aprendido muitas coisas antes de entrar em Valhalla.";
		next;
		mes "[Tomas Cerbantes]";
		mes "No entanto, posso dizer que você não está preparado para ser um Noviço.";
		mes "Lembre-se de quem você era em sua vida passada e encontre o seu caminho.";
		close;
	}
	mes "O que é que você procura?";
	next;
	if ( !checkversion(0) || checkversion(0) == -1 ){
	switch(select("Eu desejo me tornar um Noviço.:Como me torno um noviço?:Eu vim olhar os vitrais.")) {
	case 1:
		mes "[Tomas Cerbantes]";
		if (BaseJob == Job_Acolyte) {
			mes "Mas meu filho, você já é um Noviço.";
			close;
		} else if (BaseJob != Job_Novice) {
			mes "Vejo que você já tem uma classe definida, desejo sucesso a você!";
			close;
		}
		if (job_acolyte_q == 0) {
			mes "Você deseja mesmo, de todo o seu coração, se tornar um servo do Senhor?";
			next;
			if (select("Sim.:Não, eu menti.") == 2) {
				mes "[Tomas Cerbantes]";
				mes "Você mentiu?";
				mes "É bom que você tenha";
				mes "confessado seu pecado.";
				mes "Siga em paz, meu filho.";
				close;
			}
			mes "[Tomas Cerbantes]";
			mes "Muito bom. Eu aceito a vontade de "+strcharinfo(0)+" em se tornar um Noviço.";
			next;
			mes "[Tomas Cerbantes]";
			mes "Espero que você entenda que para se tornar um servo do Senhor, deverás realizar uma penitência.";
			next;
			mes "[Tomas Cerbantes]";
			switch(rand(3)) {
			mes "A sua penitência é uma forma de peregrinação.";
			default:
				set job_acolyte_q,2;
				mes "Você deve visitar o ^000077 Irmão Simão ^000000, ele se isolou com o animais perto da ^000077Abadia ao Nordeste de Prontera^000000.";
				setquest 1001;
				break;
			case 1:
				set job_acolyte_q,3;
				mes "Você deve visitar a ^000077 Irmã Matilda ^000000, ela se isolou no deserto, ^000077no mapa ao Norte de Morroc^000000.";
				setquest 1002;
				break;
			case 2:
				set job_acolyte_q,4;
				mes "Você deve visitar o ^000077 Irmão Bartolomeu ^000000, ele pode ser encontrado ^000077em uma ponte à Noroeste de Prontera^000000.";
				setquest 1003;
				break;
			}
			next;
			mes "[Tomas Cerbantes]";
			mes "Que a graça do Senhor o acompanhe em sua jornada!";
			close;
		}
		mes "Oh, você voltou!";
		next;
		mes "[Tomas Cerbantes]";
		if (getskilllv(1) < 9) {
			mes "Meu bom Deus!";
			mes "Você ainda não tem nível 9 de habilidade básicas, não é possivel servir ao Senhor assim.";
			close;
		}
		if (job_acolyte_q < 5) {
			mes "Deixe-me dizer qual será sua peregrinação.";
			next;
			switch(job_acolyte_q) {
			case 2:
				mes "[Tomas Cerbantes]";
				mes "Você deve visitar o ^000077 Irmão Simão ^000000, ele se isolou com o animais perto da ^000077Abadia ao Nordeste de Prontera^000000.";
				break;
			case 3:
				mes "[Tomas Cerbantes]";
				mes "Você deve visitar a ^000077 Irmã Matilda ^000000, ela se isolou no deserto, ^000077no mapa ao Norte de Morroc^000000.";
				break;
			case 4:
				mes "[Tomas Cerbantes]";
				mes "Você deve visitar o ^000077 Irmão Bartolomeu ^000000, ele pode ser encontrado ^000077em uma ponte à Noroeste de Prontera^000000.";
				break;
			}
			next;
			mes "[Tomas Cerbantes]";
			mes "Complete sua penitência antes de retornar.";
			close;
		}
		mes "Você completou sua peregrinação e isso enche meu coração de alegria.";
		mes "Neste momento o recebo nos caminhos do Senhor e o torno um Noviço.";
		next;
		skill 143,0,0;
		callfunc "Job_Change",Job_Acolyte;
		callfunc "F_ClearJobVar";
		completequest (checkquest(1001) != -1) ? 1001 : (checkquest(1002) != -1) ? 1002 : 1003;
		mes "[Tomas Cerbantes]";
		mes "Tenha sempre Deus em seu coração, seja nas horas de tranquilidade ou no calor da batalha, nunca se esqueça Dele, que nos ama tanto apesar de sermos imperfeitos.";
		next;
		mes "[Tomas Cerbantes]";
		mes "Do mesmo modo que você não hesitou em sua penitência, eu espero que você nunca hesite frente às tentações do pecado.";
		close;
	case 2:
		mes "[Tomas Cerbantes]";
		mes "Os pré-requisitos para se tornar um Noviço são dois.";
		mes "Por favor, ouça-os com atenção.";
		next;
		mes "[Tomas Cerbantes]";
		mes "Antes de tudo, você deve alcançar Nível 9 de Classe como Aprendiz.";
		mes "Com esse nível, será possível que você realize uma peregrinação em nome do Senhor.";
		mes "Depois você receberá um teste de penitência.";
		next;
		mes "[Tomas Cerbantes]";
		if (job_acolyte_q) {
			switch(job_acolyte_q) {
			case 2:
				mes "Você deve visitar o ^000077 Irmão Simão ^000000, ele se isolou com o animais perto da ^000077Abadia ao Nordeste de Prontera^000000.";
				break;
			case 3:
				mes "Você deve visitar a ^000077 Irmã Matilda ^000000, ela se isolou no deserto, ^000077no mapa ao Norte de Morroc^000000..";
				break;
			default:
				mes "Você deve visitar o ^000077 Irmão Bartolomeu ^000000, ele pode ser encontrado ^000077em uma ponte à Noroeste de Prontera^000000.";
				break;
			}
			next;
			mes "[Tomas Cerbantes]";
			mes "Que a graça do Senhor o acompanhe em sua jornada!";
			close;
		} else {
			mes "O destino da sua peregrinação será decidido quando você tiver preenchido o formulário.";
		}
		next;
		mes "[Tomas Cerbantes]";
		mes "Após completar estes dois requerimentos, será permitido que você se torne um servo de Deus.";
		close;
	case 3:
		close;
	}
	}
	switch(select("Mudar de classe para Noviço.:Saber os requerimentos para se tornar um Noviço.:Sair.")) {
	case 1:
		mes "[Tomas Cerbantes]";
		if (BaseJob != Job_Novice) {
			if (BaseJob == Job_Acolyte) {
				mes "Você está se sentindo bem hoje?";
				mes "Eu posso dizer pelo seu traje que você já é um Noviço.";
				mes "Você não está brincando, você está?";
			}
			else mes "Sinto muito, mas parece que você já tem a sua própria classe, não é?";
			close;
		}
		mes "Você realmente deseja se tornar um servo de Deus?";
		mes "Vamos ver se você está pronto para isso ou não... Hum...";
		next;
		mes "[Tomas Cerbantes]";
		if (getskilllv("NV_BASIC") < 9) {
			mes "Hã?! Você ainda não dominou as habilidades básicas?!";
			mes "Você tem um longo caminho a percorrer!";
			mes "Venha novamente depois de aumentar o seu nível de classe!";
			close;
		}
		mes "Hum... o seu nível de classe é suficiente...";
		mes "Bom. Agora vou dar-lhe a qualificação para se tornar um Noviço.";
		next;
		mes "[Tomas Cerbantes]";
		mes "Lembre-se de sempre ser grato a Deus, que cuida de nós o tempo todo.";
		mes "No caos e nos tempos de dificuldade enfrente tudo com fé inabalável.";
		callfunc "Job_Change",Job_Acolyte;
		getitem 1545,1;	// Maça de Iniciante
		next;
		mes "[Tomas Cerbantes]";
		mes "Por último, quero sinceramente parabenizá-lo por ser perseverante no seu julgamento de penitência.";
		close;
	case 2:
		mes "[Tomas Cerbantes]";
		mes "Você gostaria de se tornar um Noviço?";
		mes "Então, você deve preencher os seguintes requisitos pensando que essas são as práticas dadas por Deus.";
		next;
		mes "[Tomas Cerbantes]";
		mes "Primeiro, você tem que chegar pelo menos no Nível de Classe de Aprendiz 10 e aprender todas as habilidades básicas.";
		mes "Esta é a coisa mais básica que fazer, por isso é necessário considerá-la como forma de treinamento para você mesmo.";
		next;
		mes "[Tomas Cerbantes]";
		mes "Quando você achar que cumpriu esse requisito volte aqui novamente.";
		mes "Então você irá ter uma classe santa em que pode espalhar a vontade de Deus.";
		close;
	case 3:
		close;
	}
}

prt_fild03,365,255,2	script	Irmão Simão#nov	4_M_ORIENT02,{
	mes "[Simão]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 6) {
			mes "Por favor, tome cuidado.";
			mes "Eles devem saber que você me conheceu quando chegar ao Santuário de Prontera.";
			next;
			mes "[Simão]";
			mes "Enviei um pombo correio com uma mensagem.";
			mes "Espero que ele chegue em segurança...";
			close;
		}
		if (job_acolyte_q) {
			if (job_acolyte_q == 2) {
				mes "Hã...?";
				mes "Você deve ser a pessoa que aspira a se tornar um Noviço.";
				mes "Eu já recebi a notícia do Santuário que você estaria vindo.";
				next;
				mes "[Simão]";
				mes "Agora, seu nome é " + strcharinfo(0) + ", certo?";
				mes "Excelente, obrigado por me visitar.";
				next;
				mes "[Simão]";
				mes "Eu acredito que você foi mandado pelo Bispo.";
				mes "Além disso, há muitas pessoas úteis no Santuário de Prontera.";
				next;
				mes "[Simão]";
				mes "Eu acho que não há realmente nenhuma necessidade de te ensinar muito.";
				mes "Além disso, eu tenho certeza que alguém da sua geração pode ter problemas para ouvir um velho como eu. Hahaha~";
				next;
				mes "[Simão]";
				mes "Ainda assim, as lições podem vir de lugares que você menos espera.";
				mes "Deus gosta de ensinar seus filhos de maneira estranha.";
				mes "Você vai ver.";
				next;
				mes "[Simão]";
				mes "Bem, eu vou enviar a mensagem dizendo que você veio me visitar.";
				mes "Então, agora você pode voltar para o Santuário de Prontera.";
				next;
				mes "[Simão]";
				mes "Adeus.";
				close2;
				savepoint "prt_fild03",361,255;
				set job_acolyte_q,6;
				end;
			}
			mes "Ah... Você é um dos candidatos a Noviço...? Vejamos...";
			next;
			mes "[Simão]";
			mes "Seu nome é " + strcharinfo(0) + "?";
			mes "Eu acho que o seu nome não está na minha lista. Humm...";
			next;
			mes "[Simão]";
			mes "Por que você não volta para o Santuário de Prontera e verifica novamente?";
			close;
		}
		mes "Hum? O que te traz aqui?";
		mes "Este é um lugar muito perigoso para uma principiante como você!";
		close;
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherRub";
	else {
		if (BaseJob == Job_Priest) {
			mes "Parabéns.";
			next;
			mes "[Simão]";
			mes "Bem-vindo. Sinta-se livre para sentar e contemplar a mensagem de Deus comigo.";
			mes "Este lugar é bonito, mesmo se o perigo acompanha a sua sensação de serenidade...";
			close;
		}
		mes "Nossa... Você veio para até aqui para treinar?";
		mes "Ou você é apenas um Andarilho?";
		next;
		mes "[Simão]";
		mes "Seja você quem for, por favor cuide de si mesmo.";
		mes "Os monstros aqui são surpreendentemente fortes, ao contrário de sua aparência bonita.";
		close;
	}
}

moc_fild07,41,355,4	script	Irmã Matilda#2nov	4_F_SISTER,{
	mes "[Matilda]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 7) {
			mes "Vou enviar um pombo-correio para o Santuário de Prontera.";
			mes "Quando você voltar, o bispo já deverá ter recebido a minha mensagem.";
			next;
			mes "[Matilda]";
			mes "Vou orar a Deus, e espero que você se torne um Noviço em breve.";
			close;
		}
		if (job_acolyte_q) {
			if (job_acolyte_q == 3) {
				mes "Ah, você deve ser um dos candidatos a Noviço.";
				mes "Eu desejo sinceras boas-vindas.";
				next;
				mes "[Matilda]";
				mes "Qual é o seu nome? " + strcharinfo(0) + "?";
				mes "Vejamos... Ah, você está na minha lista.";
				next;
				mes "[Matilda]";
				mes "Vou enviar uma mensagem para o Santuário, confirmando que você, " + strcharinfo(0) + " me visitou e completou sua penitência.";
				next;
				mes "[Matilda]";
				mes "Por favor, retorne ao Santuário de Prontera e fale com o bispo responsável.";
				close2;
				savepoint "moc_fild07",35,355;
				set job_acolyte_q,7;
				end;
			}
			mes "Ah...! Você deve ser um dos candidatos a Noviço.";
			mes "Eu desejo sinceras boas-vindas.";
			next;
			mes "[Matilda]";
			mes "Agora, qual seu nome? " + strcharinfo(0) + "?";
			mes "Vejamos...";
			next;
			mes "[Matilda]";
			mes "Hum...";
			mes "Parece que o seu nome não está na minha lista...";
			next;
			mes "[Matilda]";
			mes "Talvez você deva retornar ao Santuário de Prontera e verificar o destino do seu julgamento de penitência mais uma vez.";
			close;
		}
		mes "...";
		close;
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_MotherMart";
	else {
		if (BaseJob == Job_Priest) {
			mes "Olá~";
			next;
			mes "[Matilda]";
			mes "Eu certamente espero que você esteja gostando de estar na graça de Deus.";
		} else
			mes "Que Deus esteja com você...";
		close;
	}
}

prt_fild00,208,218,6	script	Irmão Bartolomeu#3nov	4W_M_02,{
	mes "[Bartolomeu]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 8) {
			mes "O que?";
			next;
			mes "[Bartolomeu]";
			mes "Você tem mais algo a tratar?!";
			mes "Você não tem!";
			mes "Volte para o Santuário agora!";
			close;
		}
		if (job_acolyte_q) {
			if (job_acolyte_q == 4) {
				mes "Ei. O que quer que você seja, você parece um dos candidatos a Noviço. Certo?";
				next;
				mes "[Bartolomeu]";
				mes "Nada mal, nada mal.";
				mes "Você resistiu ao julgamento de penitência muito bem.";
				mes "Então, qual é o seu nome?";
				next;
				mes "[Bartolomeu]";
				mes strcharinfo(0) + ", huh?";
				next;
				mes "[Bartolomeu]";
				mes "Ok. Vou mandar uma mensagem para o Santuário que você, " + strcharinfo(0) + ", veio me visitar.";
				next;
				mes "[Bartolomeu]";
				mes "Agora volte para o Santuário e acabe se tornando um Noviço.";
				close2;
				savepoint "prt_fild00",206,230;
				set job_acolyte_q,8;
				end;
			}
			mes "Ei. Você parece um dos candidatos a Noviço.";
			mes "Eu estou certo?";
			next;
			mes "[Bartolomeu]";
			mes "Você percorreu todo o caminho aqui de Prontera.";
			mes "Então como você se chama?";
			next;
			mes "[Bartolomeu]";
			mes strcharinfo(0) + ", hum?";
			mes "Por que o seu nome não está na minha lista?";
			next;
			mes "[Bartolomeu]";
			mes "Você provavelmente cometeu um erro.";
			mes "Volte para o Santuário, e verifique com o bispo.";
			close;
		}
		mes "Você... Aprendiz.";
		mes "Há algo que você queira falar comigo?";
		close;
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherYos";
	else {
		if (BaseJob == Job_Priest) {
			mes "Ei...";
			next;
			mes "[Bartolomeu]";
			mes "Se você gosta, sente-se aqui comigo para meditarmos as grandes verdades.";
			mes "A majestade de Deus é verdadeiramente inspiradora...";
			close;
		}
		mes "Você tem algo a dizer?";
		mes "Porque infelizmente para você eu não tenho nenhuma resposta.";
		close;
	}
}