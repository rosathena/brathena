//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Quests Geffen                          |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.4/1.5/1.2                         |
//   |--------------------------------------------------------|
//   | Descrição: Quests dos itens para cabeça 'Máscara de    |
//   | Solda' e 'Fone de Ouvido'.                             |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Cabeçalho Criado [Holy]                            |
//   | 1.1 Script Traduzido [Barsaglini]                      |
//   | 1.2 Adicionado jogo Number Match [lututuiBR]           |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

// Quest Máscara de Solda
//============================================================
geffen_in,144,166,4	script	Ferreiro	1_M_SMITH,{
	mes "[Ferreiro]";
	mes "Você nunca ouviu falar de mim? Ora, eu sou o veterano Ferreiro desta terra. Eu passei 30 anos neste inferno quente e sangrento, dobrando ferro para o criar meu próprio aço!";
	next;
	mes "[Ferreiro]";
	mes "Mas...";
	mes "Estou envelhecendo e meu talento começa a desaparecer com a idade. Então eu tenho inventado algo para os jovens ferreiros próximos, na esperança de que eles também irão dominar a minha arte...";
	next;
	switch(select("O que é isso?:...")) {
	case 1:
		mes "[Ferreiro]";
		mes "Nós Ferreiros, devemos sempre manipular o aço sob calor extremo, ar quente e metais fundidos. Esaas condições são mais do que a maioria das pessoas podem aguentar.";
		next;
		mes "[Ferreiro]";
		mes "Para o bem do nosso artesanato, os nossos belos rostos masculinos são colocados em risco...";
		next;
		mes "[Ferreiro]";
		mes "Mas, com esta maravilha da tecnologia, não há necessidade de se preocupar! Eis então, a ^3355FFMáscara de Solda^000000!";
		next;
		mes "[Ferreiro]";
		mes "Ela funciona cobrindo seu rosto com uma grossa placa de metal. Na verdade, é um item que todo mundo deveria querer! Se não for para você mesmo, seria uma boa idéia dar a um amigo que está estudando forja.";
		next;
		mes "[Ferreiro]";
		mes "Então, o que dizer sobre isto? Se você me der ^2244FF50 Aços^000000 e ^4422FFapenas 2.000 zenys^000000, eu vou fazer isso para você imediatamente!";
		next;
		switch(select("Hm... Nada mau. Certo.:Como é que funciona mesmo?")) {
		case 1:
			if ((countitem(999) > 49) && (Zeny > 1999)){
				mes "[Ferreiro]";
				mes "Ah, vejo que você aprecia o trabalho de um ferreiro! Tudo bem, eu vou fazê-lo imediatamente!!";
				next;
				delitem 999,50;  // Aço
				set Zeny,Zeny-2000;
				getitem 2292,1;  // Máscara de Solda
				mes "[Ferreiro]";
				mes "Hahaha! Faça bom uso dela!";
				close;
			}
			else {
				mes "[Ferreiro]";
			  mes "Hmmm...";
			  mes "Você não tem os itens requeridos.";
			  next;
			  mes "[Ferreiro]";
			  mes "Um ferreiro comum iria interpretar isso como um sinal de desrespeito, mas eu vou te perdoar. Só não se esqueça o que eu preciso se você quiser mesmo fazer esta máscara.";
				close;
			}
		case 2:
			mes "[Ferreiro]";
			mes "A ^3355FFMáscara de Solda^000000 protege seu rosto, cobrindo-o com uma placa grossa de metal. Esta invenção é o resultado de 30 anos de infindável trabalho no martelo e bigorna.";
			close;
		}
	case 2:
		mes "[Ferreiro]";
		mes "Ah...!";
		mes "Eu acho que essa é a primeira vez que você encontrou um ferreiro tão bom como eu, e você fica aí... me reverenciando, mudo. Por Favor, volte quando estiver pronto para falar comigo.";
		next;
		mes "^3355FF*Esse cara parece";
		mes "um pouco cheio";
		mes "de si mesmo.*^000000";
		close;
	}
}

// Quest Fone de Ouvido
//============================================================
geffen_in,30,71,4	script	Eric	4_M_01,{
	mes "[Eric]";
	mes "Por favor, ouça";
	mes "a história do";
	mes "meu abençoado";
	mes "avô.";
	next;
	switch(select("Conversar.:Pergunte sobre os itens necessários.:Fabricar.:Sair.")) {
	case 1:
		mes "[Eric]";
		mes "Meu avô, que faleceu há alguns anos atrás, fora muito especial para mim. Ele sempre esteve comigo, e cuidou de mim. Ele era muito melhor do que meu velho, que estava sempre ocupado trabalhando.";
		next;
		mes "[Eric]";
		mes "Para mim, a biblioteca do avô sempre foi como uma terra de fantasia onde eu pudesse escapar da realidade. Passei a maior parte do meu tempo lá.";
		next;
		mes "[Eric]";
		mes "Eu aprendi um monte de coisas de seus livros na Biblioteca. Meu avô estava interessado em Alquimia e música, e sua Biblioteca encheu com muitos misteriosos livros antigos.";
		next;
		mes "[Eric]";
		mes "Um dia...";
		mes "Como de costume, fui à biblioteca e descobri um esquema interessante.";
		next;
		mes "[Eric]";
		mes "Ele mostrou alguns instrumentos que pareciam como Fones de Ouvido, quando usado na cabeça, podiam transmitir música para os ouvidos ou sussurros através de um fio fino.";
		next;
		mes "[Eric]";
		mes "Eu acho...";
		mes "É a obra inacabada do meu avô. Eu conheci alguns Alquimistas, pedi para eles produzirem, mas eles rejeitaram a minha oferta...";
		next;
		mes "[Eric]";
		mes "No final, eu pensei, tenho que fazer isso sozinho. Então, eu pesquisei por alguns anos, e agora eu compreendo perfeitamente como fazer essa coisa. Mas, eu nunca pude reunir o material para fazê-lo!";
		next;
		mes "[Eric]";
		mes "Se eu conseguir esses artigos, posso construí-lo imediatamente. Se você está interessado em minha idéia, você poderia trazê-los para mim?";
		next;
		mes "[Eric]";
		mes "Tudo que eu quero fazer é construir essa coisa em homenagem ao meu avô, e terminar o trabalho que ele começou. Se você me pedir para continuar, eu não me importaria.";
		close;
	case 2:
		mes "[Eric]";
		mes "^3355FF40 Aços^000000,";
		mes "^3355FF1 Oridecon^000000,";
		mes "^3355FF1 Álcool^000000 e";
		mes "^3355FF1 Carvão^000000.";
		mes "Estas são as coisas que eu preciso para concluir a obra do meu avô.";
		close;
	case 3:
		if ((countitem(999) > 39) && (countitem(984) > 0) && (countitem(970) > 0) && (countitem(1003) > 0)) {
			delitem 999,40; // Aço
			delitem 984,1;  // Oridecon
			delitem 970,1;  // Álcool
			delitem 1003,1; // Carvão
			getitem 5001,1; // Fone de Ouvido
			mes "*Tap! Tap! Tap!*";
			next;
			mes "[Eric]";
			mes "Obrigado.";
			mes "Por causa de você, eu pude realizar o desejo do meu avô. Se ele olhou para mim do céu agora, ele deve estar feliz.";
			next;
			mes "[Eric]";
			mes "Como eu disse, por favor, tome isto. Eu apenas estou orgulhoso de que eu posso fazer isso sozinho.";
			close;
		}
		else {
			mes "[Eric]";
		  mes "Obrigado por tentar me ajudar, mas você não trouxe todos os itens que eu preciso. Acho que você vai encontrar todos eles em breve.";
			close;
		}
	case 4:
		mes "[Eric]";
		mes "N-não?";
		mes "Bem...";
		mes "Você tem suas próprias";
		mes "preocupações, eu suponho.";
		mes "Até mais.";
		close;
	}
}

// Number Match Game :: gef_yagu
//============================================================
geffen,122,77,4	script	Nia#yagu	8_F_GIRL,{
	mes "[Nia]";
	mes "Hello, I'm Nia, the fifth";
	mes "disciple of Mawong, the";
	mes "gaming mastermind. Now,";
	mes "how would you like to play a";
	mes "Number Match game with me?";
	next;
	switch(select("Number Match Game?:Practice Number Match:Play Number Match:Record Holder:Cancel")) {
	case 1:
		mes "[Nia]";
		mes "Ah, well, the objective of";
		mes "the Number Match game is to";
		mes "guess the number I'm thinking";
		mes "of. Of course, there are a few";
		mes "rules that limit the numbers";
		mes "that I can choose from.";
		next;
		mes "[Nia]";
		mes "All the numbers that I make";
		mes "up must be 3 digits, I can't";
		mes "use 0 for any of the digits,";
		mes "and I can't use the same";
		mes "number for any of the digits";
		mes "more than once.";
		next;
		mes "[Nia]";
		mes "So, for example, I can't";
		mes "use the numbers 103, 112,";
		mes "252, or 701. Those numbers";
		mes "either have a 0, or they use";
		mes "the same number in the digits";
		mes "more than once. Got it now?";
		next;
		mes "[Nia]";
		mes "Now, you only get 5 guesses,";
		mes "and after each guess, I'll tell";
		mes "you how many digits you guessed";
		mes "correctly, followed by the number of correctly guessed digits in";
		mes "the correct digit place.";
		next;
		mes "[Nia]";
		mes "For example, let's say the";
		mes "number I come up with is 168.";
		mes "If your first guess is 678, then you guessed two of the digits,";
		mes "6 and 8, correctly. However,";
		mes "only 8 is in the correct place.";
		next;
		mes "[Nia]";
		mes "You know, it'd be a good idea";
		mes "if you try the Practice Mode";
		mes "first. Then, when you're more";
		mes "comfortable, play the actual";
		mes "Number Match game. Don't forget that speed counts for your score!";
		next;
		mes "[Nia]";
		mes "Ah, if you happen to";
		mes "make a new record in the";
		mes "Number Match game, you'll";
		mes "have your name recorded,";
		mes "so don't miss this chance for";
		mes "fame and maybe even fortune~";
		close;
	case 2:
		mes "[Nia]";
		mes "Great, let's do a practice";
		mes "Number Match game. Please";
		mes "try to guess my number, okay?";
		mes "If you want to cancel, then please enter a number greater than 1,000.";
		set .@practice,1;
		break;
	case 3:
		mes "[Nia]";
		mes "Great, let's play Number Match!";
		mes "You will have 5 chances to guess the number that I'm thinking of.";
		mes "If you want to cancel, then please enter a number greater than 1,000.";
		set .@practice,0;
		break;
	case 4:
		set .@name_record$, $050908_minus1_yagu$;
		if (.@name_record$ == "") //custom
			set .@name_record$,"(null)";
		set .@score_record, $050908_minus1_yagu;
		set .@score_min, .@score_record / 60;
		set .@score_sec, .@score_record % 60;
		mes "[Nia]";
		mes "^ff0000" + .@name_record$ + "^000000 is the";
		mes "record holder for the Number";
		mes "Match game with a time of";
		mes "^ff0000" + .@score_min + " minutes, " + .@score_sec + " seconds^000000.";
		close;
	case 5:
		mes "[Nia]";
		mes "Take care of yourself,";
		mes "adventurer. If you ever";
		mes "feel like playing a Number";
		mes "Match game, come and talk";
		mes "to me, alright? See you~";
		close;
	}
	next;

	// Generate 3 digits of a number.
	while(1) {
		set .@yagu100, rand(1,9);
		set .@yagu10, rand(1,9);
		set .@yagu1, rand(1,9);
		if ((.@yagu100 != .@yagu10) && (.@yagu100 != .@yagu1) && (.@yagu10 != .@yagu1)) {
			// Store the actual number (unofficial).
			set .@yagu, (100*.@yagu100) + (10*.@yagu10) + .@yagu1;
			break;
		}
	}

	// The official time calculation is based on ServerTime,
	// an int HHMMSS equivalent to atoi(gettimestr("%H%M%S",7)).
	// UNIX time is really a better way to do this.
	set .@start_time, gettimetick(2);

	while(1) {

		// Guess a number that meets all conditions.
		while(1) {
			if (input(.@input,100,999) != 0) {
				mes "[Nia]";
				mes "You've canceled the";
				mes "Number Match game.";
				close;
			}
			set .@input100, .@input / 100;
			set .@input10, (.@input % 100) / 10;
			set .@input1, .@input % 10;
			if ((.@input100 > 0) && (.@input10 > 0) && (.@input1 > 0)) {
				if ((.@input100 != .@input10) && (.@input100 != .@input1) && (.@input10 != .@input1))
					break;
			}
			mes "[Nia]";
			mes "You entered one of the";
			mes "digits as 0, or input the";
			mes "same numerical value in";
			mes "the digits more than once.";
			mes "Please enter another guess.";
			next;
		}

		set .@retry, .@retry+1;
		mes "[Nia]";
		mes "^ff0000"+strcharinfo(0)+"^000000,";
		mes "your guess is ^0000ff"+.@input+"^000000.";
		mes "Give me a moment to";
		mes "come up with your results.";
		next;

		// Correct digits in correct locations.
		set .@strike,0;
		if (.@yagu100 == .@input100)
			set .@strike, .@strike+1;
		if (.@yagu10 == .@input10)
			set .@strike, .@strike+1;
		if (.@yagu1 == .@input1)
			set .@strike, .@strike+1;

		// Correct digits in incorrect locations.
		set .@ball,0;
		if ((.@yagu100 == .@input10) || (.@yagu100 == .@input1))
			set .@ball, .@ball+1;
		if ((.@yagu10 == .@input100) || (.@yagu10 == .@input1))
			set .@ball, .@ball+1;
		if ((.@yagu1 == .@input100) || (.@yagu1 == .@input10))
			set .@ball, .@ball+1;

		if (.@strike == 3) {
			if (.@practice) {
				mes "[Nia]";
				mes "Yes, the number that";
				mes "I guessed was ^ff0000"+.@yagu+"^000000!";
				mes "Congratulations, you";
				mes "just won this practice";
				mes "game! Now you should be";
				mes "ready for the real thing~";
				close;
			} else {

				// Calculate final time and check against the previous record.
				set .@end_time, gettimetick(2);
				if (.@end_time < .@start_time)
					set .@end_time, .@end_time + 3600;
				set .@total_time, .@end_time - .@start_time;
				set .@total_min, .@total_time / 60;
				set .@total_sec, .@total_time % 60;
				set .@name_record$, $050908_minus1_yagu$;
				if (.@name_record$ == "") //custom
					set .@name_record$,"(null)";
				set .@score_record, $050908_minus1_yagu;
				set .@score_min, .@score_record / 60;
				set .@score_sec, .@score_record % 60;

				if (.@score_record >= .@total_time || !.@score_record) { //custom (second condition)
					set $050908_minus1_yagu$, strcharinfo(0);
					set $050908_minus1_yagu, .@total_time;
					mes "[Nia]";
					mes "Great! You guessed";
					mes "my number, which was";
					mes "^ff0000" + .@yagu + "^000000. You finished in";
					mes "^ff0000" + .@total_min + " minutes, " + .@total_sec + " seconds^000000,";
					mes "breaking the previous record";
					mes "of ^ff0000" + .@score_min + " minutes, " + .@score_sec + " seconds^000000.";
					next;
					mes "[Nia]";
					mes "^ff0000" + .@name_record$ + "^000000";
					mes "set that old record,";
					mes "but now you are the";
					mes "new person to beat in the";
					mes "Match Game. Congratulations";
					mes "for setting a new record!";
					close;
				}
				mes "[Nia]";
				mes "Nice work, you guessed";
				mes "my number! The answer, of";
				mes "course, was ^ff0000" + .@yagu + "^000000. You managed";
				mes "to guess this number correctly";
				mes "in ^ff0000" + .@total_min + " minutes, " + .@total_sec + " seconds^000000.";
				next;
				mes "[Nia]";
				mes "^ff0000" + .@name_record$ + "^000000 is";
				mes "the current Number Match";
				mes "record holder with a time";
				mes "of ^ff0000" + .@score_min + " minutes, " + .@score_sec + " seconds^000000.";
				mes "Good luck, and hopefully you";
				mes "can break this record someday~";
				close;
			}
		} else {
			if (.@practice) {
				mes "[Nia]";
				mes "Your guess has ^ff0000" + .@ball + "^000000 of the";
				mes "correct numbers. ^ff0000" + .@strike + "^000000 of the";
				mes "digits in your guess have";
				mes "the correct number in the";
				mes "correct digit placement.";
				mes "Alright, try it again~";
				next;
			} else {
				mes "[Nia]";
				mes "Your last guess had ^ff0000" + .@ball + "^000000";
				mes "of the correct numbers,";
				mes "and ^ff0000" + .@strike + "^000000 digits in your guess";
				mes "had the correct number in";
				mes "the correct digit placement.";
				switch(.@retry) {
					case 1: mes "You have four guesses left."; break;
					case 2: mes "You have three guesses left."; break;
					case 3: mes "You have two guesses left."; break;
					case 4: mes "You only have one guess left..."; break;
					case 5: mes "The answer was ^ff0000" + .@yagu + "^000000."; close;
				}
				next;
			}
		}
	}
}